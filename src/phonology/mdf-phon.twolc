!! !!!The Moksha morphophonological/twolc rules file 


Alphabet !!≈!!The base @CODE@
!! {{{
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я !!= @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я !!= @CODE@
!! }}}

%{ъØ%}:0	
!! This will represent schwa in first syllable

!! 2016-03-12
%^А2О:0		!!= @CODE@ used with final а in пула 


%{еэ%}:е
%{еэ%}:э

!Suffix final variant vowel for hard vs soft
!Ь2:ь !
Ь2:0 !
 %{ЬØ%}:0	!!= @CODE@ after imperative Sg2
! verbal stem consonant that appear in PretI Sg3
!Т1:0 !
 %{ВИУ%}:в       !!= @CODE@ lative
 %{ВИУ%}:и
 %{ВИУ%}:у

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !


! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
А2:я !А2
 %{АЯ%}:я !!= @CODE@ А2 in панемс:панян
!Ы2:и !
Ы2:ы !
 %{ИЫЙ%}:0	!!= @CODE@ phasing out Ы2, eg кундамс:кундай, панемс:пани, кандомс:канды

! onset vowel  for morphemes requiring a preceding vowel
 %{ЕОØ%}:е	!!= @CODE@ morpheme onset linking vowel from %^О1
 %{ЕОØ%}:о	!!= @CODE@ morpheme onset linking vowel from %^О1
 %{ЕОØ%}:0	!!= @CODE@ morpheme onset linking vowel from %^О1 also пинге


! derivational suffix protovowel  "mo" and "me"


! пула, офта, пинге
!О2:а
!О2:о
!О2:е
%{АЕØ%}:а	!!= @CODE@  ConNeg with ашень, тят, афоль
%{АЕØ%}:е
%{АЕØ%}:0

%{АО%}:а	!!= @CODE@  пула stem
%{АО%}:о	!!= @CODE@  пула stem
%{АОØ%}:а	!!= @CODE@  офта stem
%{АОØ%}:о	!!= @CODE@  офта stem
%{АОØ%}:0	!!= @CODE@  офта stem

 %{ЕО%}:е	!!= @CODE@ сельме
 %{ЕО%}:о	!!= @CODE@ 

 %{ЕØ%}:е	!!= @CODE@ кече
 %{ЕØ%}:0	!!= @CODE@ 

%{АЕ%}:е
%{АЕ%}:а	!!= @CODE@ prolative

! devoicing
Х1:х
!Х1:0
 %{Х%}:0	!!= @CODE@ This usually precedes the plural marker

%{DIM%}:н		!!= @CODE@ This will be for diminutive initial consonant

! clitic proto consonant
%{гк%}:г !
%{гк%}:к !
%{гк%}:0 !



! Prolative Ga
%{вгк%}:г !
%{вгк%}:к !
%{вгк%}:в !


! Imperative clitic proto consonant
Г4:г !
Г4:к !
Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
К1:к !
!К1:т 
!
%{КТ%}:т	!!= @CODE@ used in imperative and connegative
%{КТ%}:к
! connegative suffix rendering vowel
! панемс+V+Imprt+ScSg2
!!€ пань%>%{Х%}%{КТ%}%{ЬØ%}
!!€ пат0%>0тье0
К3:0 !

! Stem final on Imperative Verbs
!!К4:0 !



!! in ped realized as hard sign

!! !!TRIGGERS
%^RmVow:0 !!= * @CODE@ for removing vowels 
%^Ь2ZERO:0	!!= @CODE@ removes soft sign before vowel or other combination
%^Ъ2PED:0	!!= @CODE@ brings out hard sign, which is
!! otherwise automatically removed %{ъØ%} 
%^Е2О:0		!!= @CODE@ Change stem-final vowel сембе:сембось
%^Е2А:0		!!= @CODE@ Change stem-final vowel мельге:мельганза
%^Я2А:0		!!= @CODE@ Change stem-final vowel рьвя:рьванц
%^А2Е:0		!!= @CODE@ Change stem-final vowel мокша:мокшесь
%^Devoice:0	!!= @CODE@ for devoicing изь:исть
%^PXSG1:0		!!= @CODE@ кядне, ярмаконе не vs %{ЕОØ%}не

TLoss:0
SLoss:0
KLoss:0
VLoss:0
WLoss:0
NoVLoss:0

 %- !!= * {{@CODE@}} – Hyphen with constructions like "-buti"
 %> !!= * {{@CODE@}} – conjugation/declension morpheme boundary suffix
 »  !!= * {{@CODE@}} – derivation morpheme boundary suffix
 #  !!= * {{@CODE@}} – word boundary 

;


Sets !!≈ !!The @CODE@

!! {{{
Vows = а о у ы э я ё ю и е ;                                            !!= @CODE@
verbStemVows = а о э я ё е ;                                            !!= @CODE@
verbStemVowStrong = а о э я ё е ;                                       !!= @CODE@
noGlideVow = а о у ы э ;                                                !!= @CODE@
glideVow = я ё ю и е ;                                                  !!= @CODE@
BTV = а о у я ё ю ;                                !!= @CODE@ ! Back Trigger Vowels
FTV = ы и е э ;                                   !!= @CODE@ ! Front Trigger Vowels

Cns = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;                       !!= @CODE@
NPC = б в г д ж з к л м н п р с т ф х ц ч ш щ ; ! Nonpalatal Consonants !!= @CODE@
CnsVoiced = б в г д ж з й л м н р ;                                     !!= @CODE@
CnsVoicedDent = д з л н р ;                                             !!= @CODE@
CnsVoicedNonDent = б в г ж й м ;                                        !!= @CODE@
CnsVoiceless = к п с т ф х ц ч ш щ ;                                    !!= @CODE@
CnsVoicelessDent = с т ц ;                                              !!= @CODE@
CnsVoicelessNonDent = к п ф х ч ш щ ;                                   !!= @CODE@
CnsNonDent = б в г ж к м п ф х ч ш щ ;                                  !!= @CODE@
CnsDent = д з л н р с т ц  ;                                            !!= @CODE@
CnsDentNoL = д з н р с т ц  ;                                           !!= @CODE@

Letters = Vows Cns ь ъ ;                                                !!= @CODE@
!! }}}

Definitions !!≈ !!The @CODE@

CnsNotCliticVok = [ [Cns - в] | в [ Vws - о ] | в о [ Cns - к] ] ;

! Front Trigger Consonants (less ль)
FTC = [ й: | д ь: | з ь: | н ь: | р ь: | с ь: | т ь: | ц ь: ] ;

! All Consonants
AC = [ FTC | л ь | NPC ] ;

! Left Front Context Soft
LFCS = [ [ FTV AC* [ Cns - CnsDent ] ] | FTV AC* FTC | [ BTV FTC [ AC* [ Cns - CnsDent ] ]* ] ] ;

! Left Front Context Hard
LFCH =[ [ :FTV AC* CnsDent ] | [ BTV FTC [ AC* CnsDent ]+ ] | [ BTV л ь [ к с ] ] ] ;

! Left Back Context
LBC = [ [ BTV ( %> ) NPC:+ ] | [ BTV л ь [[ NPC* NPC ] - [ :* к с ] ] ] ] ;

PossX = [ м | н %{ЬØ%}: | т %{ЬØ%}: | н т %{ЬØ%}:  | н к | с т ] ;

! Suffix-Initial Variable Vowel
SIVV = [ PossX | в | н е н ь | т е н ь | т е т ь  | н т е н ь | л ь | л и | к с | ш к а | с | %{ЕОØ%}: н з %{АО%}: | т ь | н т ь: | н ь: | м А1: | м г а | т с ь | т с т ь | з ь: ] ;


Back =  :* [SubseqBackHarm \[ :FTV | :й | :ь ]* ( л ь ) [NPC]]  ;





Rules !!≈ !!The @CODE@

!!!Vowel changes

!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
!! __@RULENAME@__  
 Vx:Vy <=> [ Cns | Vows ] _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]] ;
!!2013-10-16!!                   Cns  _ К4:0 %>  %{ИЫЙ%}: ;
          where Vx in (а о э ы я ё ю)
                Vy in (0 0 0 0 0 0 0) matched ;
!!цёра+N+Sg+Nom+Ind+Prs+ScSg1: __man/mies__
!!€ цёра%>%{АЯ%}н
!!€ цёр0%>ан

"у:0"
!! __@RULENAME@__
 Vx:Vy <=> :Vows Cns+ _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]] ;
           where Vx in (и у)
                 Vy in (0 0) matched ;

!! тумс+V+Ind+Prs+ScSg1
!!$ #ту%>%{АЯ%}н
!!$ #т0%>ан
!!€ #ту%>%{АЯ%}н
!!€ #ту%>ян
!
!Vowels in Nouns and Adjectives 
!2006-12-02
!"YI3:y"
!И3:ь <=>  CnsDent  _ [%>  [%{гк%}:г|%{гк%}:к|%{вгк%}:г|%{вгк%}:к|с [%> |# ] | с (т) %{АО%}: |:CnsNonDent]| #] ;
!      #   :* л  _ [%> (в:0) :Cns|#] ;
!


! Deverbal noun in ОмА
"A1:o"
!! __@RULENAME@__  
А1:о <=>  [ а |я ] %>  м  _ ;

!!2013-10-16!"%{ИЫЙ%}:yi"
!!2013-10-16!!! __@RULENAME@__  
!!2013-10-16!%{ИЫЙ%}:и <=> [ я:0 | ё:0 | е:0 | ю:0 | ь:0 | й:0 | CnsNonDent (noGlideVow:0)]  %> _ ;

"%{ИЫЙ%}:j"
!! __@RULENAME@__  
%{ИЫЙ%}:й <=> [ я | а | о ]  %> _ [ # | » | %> в о к | ( %> ) х т  ь | х т е | н е | т ь ] ;

"%{ИЫЙ%}:ы"
!! __@RULENAME@__
%{ИЫЙ%}:ы <=> CnsDent %> _  ;

"%{ИЫЙ%}:и"
!! __@RULENAME@__
%{ИЫЙ%}:и <=> [ CnsNonDent | CnsDent ь: | е: | й: ] %> _  ;


"%{ЕОØ%}:о"
!! __@RULENAME@__
%{ЕОØ%}:о <=> [ б | в | г | к | м | п | ф | х ] (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [ д | с | т | ц ] (%^PXSG1:) %> _  Cns ;
 [з |л|н|р ] (%^PXSG1:) %> _ [Cns - [к|с]] ;
  [з|л|н|р ] %> _ [с ь к |с т ] ;
  Cns _ (%^PXSG1:) %> [Cns | %{ЕОØ%}:] ;



"%{ЕОØ%}:е"
!! __@RULENAME@__
%{ЕОØ%}:е <=>
   [ ж | ч | ш | щ ] (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
   й: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
!! вай+N+Sg+Nom+PxSg1 __oil,butter/voi__
!!€ вай%>%{ЕОØ%}зе
!!€ ва0%>езе
!   й: %> _ [Cns - [ к|с|ц|з]] ;
 [ д | с | т | ц ] ь: (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [з | л|н|р ] ь: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
 [з |л|н|р ] ь: %> _ [с ь к |с т ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти0%>еза
   Cns (ь:) _ # ;
!!€ пинг%{ЕОØ%}#
!!€ пинге#
!!€ эрьхк%{ЕО0%}#
!!€ эрьхке0

"%{ЕОØ%}:0"
!! __@RULENAME@__
%{ЕОØ%}:0 <=>  [Vows|%{АО%}:|%{АОØ%}:|%{АЕ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:] (%^Е2А:) %> _ Cns ;
        [з|й|л|н|р] (ь:) %> _  [к с | с [#|%>]] ;
          Cns (ь:) _ %^RmVow: ;

"%{АОØ%}:о"
!! __@RULENAME@__
%{АОØ%}:о <=>
        Cns _ %> ;
![ б | в | г | к | м | п | ф | х ] %> _  [Cns|%{вгк%}:] ;
! [ д | с | т | ц ] %> _  Cns ;
! [з |л|н|р ] %> _ [Cns - [к|с]] ;
!  [з|л|н|р ] %> _ [с ь к |с т ] ;


"%{АОØ%}:а"
!! __@RULENAME@__
%{АОØ%}:а <=>
   Cns  _ # ;
!!€ панг%{АОØ%}#
!!€ панга#
!   [ ж | ч | ш | щ ] %> _  Cns ;
!   й: %> _ [Cns - [ к|с|ц|з]] ;
! [ д | с | т | ц ] ь: %> _  Cns ;
! [з | л|н|р ] ь: %> _ [Cns - [ к|с]] ;
! [з |л|н|р ] ь: %> _ [с ь к |с т ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти0%>еза

"%{АОØ%}:0"
!! __@RULENAME@__
%{АОØ%}:0 <=>
          Cns _ %^RmVow: ;
!! офта+N+Sg+Nom+Def __bear/ohto, karhu__
!!€ офт%{АОØ%}%^RmVow%>сь
!!€ офт00%>сь

![Vows|%{АО%}:|%{АЕ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:]  %> _ Cns ;
!        [з|й|л|н|р] (ь:) %> _  [к с | с [#|%>]] ;

"%{АО%}:о"
!! __@RULENAME@__
%{АО%}:о <=>
        Cns _ %> [%{ЕОØ%}:|%{DIM%}:|%{Х%}: т|Cns] ;
!! пула+N+Sg+Nom+Def: __tail__
!!€ пул%{АО%}%>сь
!!€ пуло%>сь
!!€ пул%{АО%}%>%{ЕОØ%}нз%{АО%}
!!€ пуло%>0нза


"%{АО%}:а"
!! __@RULENAME@__
%{АО%}:а <=>
   Cns  _ # ;
!!€ пул%{АО%}#
!!€ пула#

"Vx:0 %{АО%}:0"
!! __@RULENAME@__
Vx:0 <=>
        Cns _ %> %{АЯ%}:  ;
	where Vx in (%{АО%} %{ЕО%}) ;
!! пря+N+SP+Ine+Der/Cop+Prs+ScSg1 __top, head/pää__
!!€ пря%>с%{АО%}%>%{АЯ%}н
!!€ пря%>с0%>ан


"%{ЕО%}:е"
!! __@RULENAME@__
%{ЕО%}:е <=>  _ # ;
!!€ сельм%{ЕО%}#
!!€ сельме#
      [ ш|ч] %> _ %> ш к а ;

"%{ЕО%}:о"
!! __@RULENAME@__
%{ЕО%}:о <=>  [Cns - [ ш|ч|ж]]  _ %> [%{ЕОØ%}:| Cns:|:Cns|%{вгк%}:| %{Х%}:] ;
!!€ сельм%{ЕО%}%>%{ЕОØ%}нь
!!€ сельмо%>0нь
       [с|ц] %> _ %> ш к а ;

"%{ЕØ%}:е"
!! __@RULENAME@__
%{ЕØ%}:е  <=>  _ [%> |#] ; 

"%{ЕØ%}:0"
!! __@RULENAME@__
%{ЕØ%}:0  <=>  _ %^RmVow: ;
!! тяште+N+Pl+Nom+Def __star/tähti__
!!€ тяшть%{ЕØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ тяшт000%>0ть


"0:j"
!! __@RULENAME@__  
 0:й <=> %> [ %{ИЫЙ%}:ы | %{ИЫЙ%}:и ] %> _  х т [ :ь ]  ;

"е:0"
!! __@RULENAME@__
 е:0 <=>  _  %> %{ИЫЙ%}: [ # | %> [в о к|%{гк%}: %{АЕ%}:] | (%>)  х т  :ь |н | х т е|т]  ;
         [ Cns | Vows ] _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]|%{ВИУ%}:] ; 

!!!!!

"е:о"
!! __@RULENAME@__
 е:о <=> Cns: _ %^Е2О: %> (%{АО%}:|%{Х%}:) Cns: ;

! CHECKME
"е:а"
!! __@RULENAME@__
 е:а <=> Cns: _ %^Е2А: %> (%{АО%}:|%{Х%}:|%{ЕОØ%}:) Cns: ;

"а:е"
!! __@RULENAME@__
 а:е <=> Cns: _ %^А2Е: %> ;

"а:о"
!! __@RULENAME@__
 а:о <=> Cns: _ %^А2О: %>  ;

"я:а"
!! __@RULENAME@__
 я:а <=> Cns: _ %^Я2А: %> ;
!!рьвя+N+Sg+Gen+PxSg3: __wife__
!!€ рьвя%^Я2А%>нц
!!€ рьва0%>нц

!!€ ава%>з%>%{ЕОØ%}нт
!!€ ава0з0онт


"%{АЕØ%}:а Always"
!! __@RULENAME@__  
 %{АЕØ%}:а <= [Cns - [ ш | ч | ж | щ | щ | й |в|д | л | з]] %> _ [ # | %- ]  ;
 	      Cns [д | л | з]  %> _  [ # | %- ] ;

"%{АЕØ%}:а Sometimes"
!! __@RULENAME@__  
 %{АЕØ%}:а => [Cns - [ ш | ч | ж | щ | щ | й |в|д | л | з]] %> _ [ # | %- ]  ;
              [д | л | з]  %> _  [ # | %- ] ;

"%{АЕØ%}:е Always"
!! __@RULENAME@__  
 %{АЕØ%}:е <= [ ш | ч | ж | щ | й: |[Cns - [ д | л | з]] ь:] %> _  [ # | %- ] ;
 	      Cns [д | л | з] ь: %> _  [ # | %- ] ;

"%{АЕØ%}:е Sometimes"
!! __@RULENAME@__  
 %{АЕØ%}:е => [ ш | ч | ж | щ | й: |[Cns - [ д | л | з]] ь:] %> _  [ # | %- ] ;
              [д | л | з] ь: %> _  [ # | %- ] ;

"%{АЕØ%}:0 Always"
!! __@RULENAME@__  
 %{АЕØ%}:0 <=
    [ Vows | в ]  %> _ [ # | %- ]  ;

"%{АЕØ%}:0 Sometimes"
!! __@RULENAME@__  
 %{АЕØ%}:0 =>
    Vows ([ в | д | л | з] (ь:)) %> _ [ # | %- ]  ;



"%{АЕ%}:a"
!! __@RULENAME@__  
 %{АЕ%}:а <=>
   [ [Vows - [и]] | [Cns - [й]] | %{ЬØ%}:0| ъ |:Vows - [:и]] %> [%{вгк%}:|%{гк%}:]  _ [ # | %> ] ;
!! вал+Hom1+N+Sg+Nom+Indef+Foc
!!€ вал%>%{гк%}%{АЕ%}#
!!€ вал%>га#
!! мельге+Pron+Prl+PxSg3: __after__
!!€ мельг%{АЕ%}%>%{ЕОØ%}нз%{АО%}
!!€ мельга%>0нза
!!$ мельг%{АЕ%}%>%{ЕОØ%}нз%{АО%}
!!$ мельге%>0нза

   [ч|ш|ж|ч] _ # ;
!! мокша+N+Sg+Nom+Def: __Moksha__
!!€ мокш%{АЕ%}%>сь
!!€ мокше%>сь



"%{АЕ%}:е"
!! __@RULENAME@__  
 %{АЕ%}:е <=> [ :ь| й] %> [%{вгк%}:|%{гк%}:] _ [ # | %> ] ;
!! менель+N+SP+Prl+Indef __sky/taivas__
!!€ менель%>%{вгк%}%{АЕ%}
!!€ менель%>ге
!!€ шяй%>%{вгк%}%{АЕ%}
!!€ шяй%>ге
     и %> [%{вгк%}:|%{гк%}:|н г] _ [ # | %> ] ;
!! аля+N+Sg+Dat+PxSg1+Foc __father/isä__
!!€ аля%>%{ЕОØ%}з%>ти%>%{гк%}%{АЕ%}
!!€ аля%>0з%>ти%>ге
   [ч|ш|ж|ч] _ %> ;

"%{АЯ%}:ya"
!! __@RULENAME@__
%{АЯ%}:я <=>     [ CnsDent: [ я:0 | е:0 | ь: ]| й: | # Cns* [у|и]] %>  _  [ т | н ] ;
!!€ пань>%{АЯ%}н
!!€ пан00ян
!! тумс+V+Ind+Prs+ScSg1 __leave/lähteä__
!!€ ту>%{АЯ%}н
!!€ ту>ян

   [ CnsDentNoL ь: [ г: ] | ь: | е | й ] %> %{Х%}: т _ [ д | м | н ] ;
!!€ пань>%{Х%}т%{АЯ%}ма
!!€ пан000тяма
!!€ мярьг%>%{Х%}т%{АЯ%}ма
!!€ мярьк%>0тяма


"%{АЯ%}:a"
!! __@RULENAME@__  
%{АЯ%}:а <=> [ :BTV | LBC | CnsDent: ] %>  %{Х%}: т  _  [ д | н | м ] ;
    [[ CnsDent | CnsNonDent ] ( а:0 | о:0 )| %{ЕО%}:|%{АЕ%}:|%{ЕОØ%}:| %{АОØ%}:| %{АО%}: ] %>  _  [ т | н ] ;
!!€ канд>%{АЯ%}н
!!€ канд0ан
  щ е: %> _ [ т | н ] ;
!!€ аще%>%{АЯ%}н
!!€ ащ0%>ан



"j:0"
!! __@RULENAME@__  
й:0 <=> Vows _ [%{ВИУ%}:| %> %{ЕОØ%}: | %>  %{АЕØ%}:| %> %{ИЫЙ%}:и|   %> [%{АЯ%}:я | %{АО%}: [ :Cns | К3:0 | #]] ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти00еза
   Vows Cns+ [и|ы] _ %> %{Х%}: т: ;
!! седи+N+Pl+Nom+Indef __heart/sydän__
!!€ седий%>%{Х%}т%{ЬØ%}
!!€ седи0%>хть

!"й:х"
!! __@RULENAME@__
!й:х <=>
    _ %> %{Х%}: т %{АЯ%}: [ м|д] а ;
! CHECKME 2017-03-07


!
!18.04.2007 Adding "U2:" and contemplating environment "%> т н _"
!
"ye:e always"
!! __@RULENAME@__  
%{еэ%}:э <=
          CnsDent (%{АО%}:0) %>  н _ [[ # | %-|%> %{гк%}:0 а:я]| н ь: | к] ;
!          [[[Back - [:* Vows]] %>| CnsDent]  %> ] т: н _ [ # | %- |%> [%{гк%}:0 а:я |т| л|%{вгк%}:в а | в | д | к | н | с | ш ]];


"ye:e sometimes"
!! __@RULENAME@__  
%{еэ%}:э =>
          [CnsDent|CnsNonDent] ( %{АО%}:0 ) %>  н _ [[ # | %-|%> %{гк%}:0 а:я]|н ь:|к ] ;
!          [ CnsDent | CnsNonDent | м:0 ]  ( %{АО%}:0 | %{АОØ%}:0 ) %> ( VLoss:0 ) ( TLoss:0 ) т: н _ [ # | %- | %> [%{гк%}:0 а:я | т | л | %{вгк%}:в а | в | д | к | н | с | ш ]];


"a:ya"
!! __@RULENAME@__  
а:я  <=> :Vows ((%>) К3:0 )  %> %{гк%}:0 _  ;


!! !CONSONANT CHANGES
" %{Х%}:0 after vowel"
!! __@RULENAME@__  
%{Х%}:0 <=> [ :Vows (%^Е2О:|%^А2О) |з:|ж:| т ( ь: ) | к |г:| с | ш | ц | м:|в: | п | ч | х | щ | ф | н: ( ь: ) | д: ( ь: ) | г:к ] %> _ ;
	 [ з:|ж:| т ( ь: ) | к |г:| с | ш | ц | п | ч | х | щ | ф | м:|в: | н: ( ь: ) | д: ( ь: ) | г:к ] [%{ЕОØ%}:|%{ЕØ%}:|%{АОØ%}:] %^RmVow: %> _ ;
!! валда+N+Pl+Nom+Def __light/valo__
!!€ валд%{АОØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ валт00%>0т0


" %{Х%}:х after some consonants"
!! __@RULENAME@__  
%{Х%}:х <=> [ р | л | й: ] ( ь: )  %> _ ;
!! ваны+N+Pl+Nom+Def __watcher/katsoja__
!!€ ваный%>%{Х%}тне
!!€ ваны0%>х0не

"м:п ям"
!! __@RULENAME@__  
м:п <=> _  ( ь: ) %> %{Х%}: [ т: | %{КТ%}: ]  ;
!!€ ям%>%{Х%}т%{ЬØ%}
!!€ яп%>0т0

"в:ф ям"
!! __@RULENAME@__  
в:ф <=> _  ( ь: ) %> %{Х%}: [ т: | %{КТ%}: ]  ;
!!€ кев%>%{Х%}т%{ЬØ%}
!!€ кеф%>0т0

"н:т окун"
!! __@RULENAME@__  
н:т <=> _  ( ь: ) %> %{Х%}: [ т: | %{КТ%}: ]  ;

!2016.QQ
!Ablative d/t
"d:t"
!! __@RULENAME@__  
д:т  <=> [ CnsVoiceless | д | г] (ь:0) (%{ЕОØ%}: %^RmVow:)  %> _ [ %{АО%}: ] ;
!__д:т од:оттне definite plural__
          _ (ь:)  [([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> [ %{Х%}: [ т: н е | т я | т %{АЯ%}: | т %{ЬØ%}: ] | %{КТ%}: | к ]| %^Devoice: ] ;
!!€ канд%>%{Х%}тя
!!€ кант%>0тя
!!€ канд>%{Х%}%{КТ%}%{ЬØ%}
!!€ кант%>0т0
!!€ лемдь%>%^Devoice%>ф
!!€ лемть%>0%>ф
!!€ пильг%{ЕОØ%}%^RmVow%>д%{АО%}
!!€ пильг00%>та


"З:С"
!! __@RULENAME@__
 з:с <=> Vows _ (ь:) [%^Devoice:|([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> %{Х%}:] ;
!! изь+V+Aux+Neg+Ind+Prt1+ScPl3:	__they did not__
!!€ изь%^Devoice%^Ь2ZERO%>ть
!!€ ис000%>ть

"ж:ч панчсь"
!! __@RULENAME@__
ж:ч <=> _  %> с (т) ь  ;
!!€ панж%>сь
!!€ панч%>сь


"г:к"
!! __@RULENAME@__
г:к <=>
!"д:т од:оттне definite plural"
          _ ([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:)  %> [ %{Х%}: [ т: н е | т я | т %{АЯ%}: | т %{ЬØ%}:| %{КТ%}: ] | к ];
!!€ мярьг%>%{Х%}т%{АЯ%}ма
!!€ мярьк%>0тяма
! WHAT IS THIS      CnsVoiceless (ь) %> _ %{АЕØ%}: # ;



"с:ц ломань:ломанць"
!! __@RULENAME@__  
с:ц <=> н ( ь: ) %> _ [ ь | а | ы | ( т ) %{АО%}: | н | %> | # ] ;

!Changes in suffix consonants

!!!Depricated due to 
!!ERROR: Label not found in 'EQV_LABELS' (114) **
!!Solved in several contexts by declaring both lexical
!!and surface forms in the alphabet.
"G1:0"
!! __@RULENAME@__  
%{гк%}:0 <=> [ [:Vows - [:и]] ((%>) К3:0 )  %> | К1: %{ЬØ%}:0 %> KLoss:0 | :к %> KLoss:0 ] _ а: ;


"G1:g"
!! __@RULENAME@__  
%{гк%}:г <=> [CnsVoicedDent (:ь|%{ЬØ%}:0)| й | и | [ CnsVoicedNonDent - г ] ( :ь )] %> _ [ а|%{АЕ%}:] ;
!! вал+Hom1+N+Sg+Nom+Indef+Foc __word/sana__
!!€ вал%>%{гк%}%{АЕ%}
!!€ вал%>га
!! аля+N+Sg+Dat+PxSg2+Foc __father/isä__
!!€ аля%>цти%>%{гк%}%{АЕ%}
!!€ аля%>цти%>ге

"G1:k"
!! __@RULENAME@__  
%{гк%}:к <=>  [ CnsVoicelessDent ( :ь | %{ЬØ%}:0 | %{ЬØ%}:ь ) | CnsVoicelessNonDent | г | т %{ЬØ%}: | К1: %{ЬØ%}: | К4:0 ] :0* %> _ [а|%{АЕ%}:]  ;
!! вал+Hom1+N+SP+Ill+Indef+Foc __word/sana__
!!€ вал%>с%>%{гк%}%{АЕ%}
!!€ вал%>с%>ка


!Lative
"%{ВИУ%}:v"
!! __@RULENAME@__
 %{ВИУ%}:в <=> [а | я] %> _ ;

!Lative
"%{ВИУ%}:i"
!! __@RULENAME@__
 %{ВИУ%}:и <=> [е: | ь: | и:] %> _ ;

!Lative
"%{ВИУ%}:u"
!! __@RULENAME@__
 %{ВИУ%}:у <=> [Cns] %> _ ;

!Prolative
"G2:g"
!! __@RULENAME@__  
%{вгк%}:г <=> [ CnsVoicedDent ( :ь )|и| [ CnsVoicedNonDent - г ] ( :ь ) ] %> _ %{АЕ%}: ;


"G2:v"! 
%{вгк%}:в <=>  [:Vows - :и] %>  _ %{АЕ%}: ;


"G2:k"
!! __@RULENAME@__  
%{вгк%}:к <=>  [ CnsVoicelessDent ( :ь )| CnsVoicelessNonDent ( :ь )| г ] %> _ %{АЕ%}: ;

! 2012-08-07
"G4:g" !Imperative Clitics
Г4:г  <=> з ь %> _ а ;



"G4:0"
!! __@RULENAME@__  
Г4:0  <=>
           [ К1: %{ЬØ%}: | к ] %> _ а ;
  [%{АО%}: ] %> _ а: ;

"G4:k"
!! __@RULENAME@__  
Г4:к  <=>  # [и л я|у ж о|а з ё|а д я|а й д а] %> _ а  ;


"m:0"
!! __@RULENAME@__  
м:0 <=> 
 BTV  _ %{АОØ%}:0  %>  %^RmVow: [ т| т н е ] ;



!2016.QQ
!! !imperative suffix
"К1 %{КТ%}:t left"
!! __@RULENAME@__  
 %{КТ%}:т <= [ Vow н: | CnsVoiceless ] ( ь: ) %> (%{Х%}:) _  ;
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть

"К1 %{КТ%}:t right"
!! __@RULENAME@__  
 %{КТ%}:т =>  [ н: | CnsVoiceless | д: ] ( ь: ) %> (%{Х%}:) _  ;
!!€ канд>%{Х%}%{КТ%}%{ЬØ%}
!!€ кант00т0
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть

!2016.QQ
"К1 %{КТ%}:к left"
!! __@RULENAME@__  
 %{КТ%}:к <=  [ Vows ] %> (%{Х%}:) _  ;
! %> %{Х%}: [ т | %{КТ%}: ] 
!!€ кунда>%{Х%}%{КТ%}%{ЬØ%}
!!€ кунда00к0

"К1 %{КТ%}:к right"
!! __@RULENAME@__  
 %{КТ%}:к =>  [ Vows | д: ( ь: ) ] %> (%{Х%}:) _  ;
! %> %{Х%}: [ т | %{КТ%}: ] 
!!€ кунда>%{Х%}%{КТ%}%{ЬØ%}
!!€ кунда00к0

!2016.QQ
! Suffix final hard vs soft
"%{ЬØ%}:ь"
!! __@RULENAME@__  
%{ЬØ%}:ь <=> [ ь:|й:] ([%{ЕØ%}:] %^RmVow:) %> (%{Х%}:) [ т: | %{КТ%}: ] _ ;

!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть
!2016.QQ

!! Plural before definite plural following a consonant
"t:0"
!! __@RULENAME@__  
т:0 <=>
[б:|в:|ж:|з: ( ь: )| й |г:|к| л ( ь: ) |м: |п| р ( ь: ) |с ( ь: ) |ф|х|ц ( ь: ) |ч|ш|щ] ([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> %{Х%}: _ н е ;
!! Ананьина К.И. 2000: 55
!!€ ава%>%{Х%}тне
!!€ ава%>0тне
!!€ шуфт%>%{Х%}тне
!!€ шуфт%>0тне
!!€ нармот0%>0тне
!!€ лав%>%{Х%}тне
!!€ лаф%>00не
!!$ лав%>%{Х%}тне
!!$ лаф%>0тне

![б:|д: ( ь: ) |ж:|з: ( ь: )| й |г:|к| л ( ь: ) |м:|н: ( ь: ) |п| р ( ь: ) |с ( ь: ) |т ( ь: ) |ф|х|ц ( ь: ) |ч|ш|щ] %> %{Х%}: _ н е ;

!!SLoss s s > s = s 0 0 s
"s:0"
!! __@RULENAME@__  
с:0 <=> с _ %> SLoss:0 с ;

!!€ класс%>SLossс
!!€ клас000с
!2016.03!




!Word final Vowels

!2016.QQ
"y:0"
!! __@RULENAME@__  
 ь:0 <=> 
  [ н: | д: | т ] _ (%{ЕØ%}: %^RmVow:) %> (%{Х%}:) [ т: | %{КТ%}: ] ; 
!! plural
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть
 CnsDent: _ ( %>|%^PXSG1: %>) [ %{АЯ%}: | %{ИЫЙ%}: | %{ЕØ%}:е | %{ЕОØ%}: | (%^Ь2ZERO:) %{ВИУ%}: ] ;
!!€ мяль%>%{ЕОØ%}зе
!!€ мял0%>езе
!! мирде+N+Sg+Nom: __husband/aviomies__
!!€ мирдь%{ЕØ%}#
!!€ мирд0е#
    _ %> %{АЕØ%}:е ;

 CnsDentNoL _ %> [ф т %{ЕОØ%}: м [ %{ЕОØ%}: ] | К1: | н | т | с: (т) [ ь: | е: ]] ;
!2018-07-01 CnsDentNoL _ %> [ф т %{ЕОØ%}: м [ %{ЕОØ%}: ] | К1: | н | д: | т | с: (т) [ ь: | е: ]] ;
 Cns: _ (%^Devoice:) %^Ь2ZERO: ;
!! изь+V+Aux+Neg+Ind+Prt1+ScPl3: __they did not__
!!€ изь%^Devoice%^Ь2ZERO%>ть
!!€ ис000%>ть
!2016.QQ
!! ломань+N+SP+Abl+Indef:
!!$ ломань%>д%{АО%}
!!$ ломань%>да

!! !DIMINUTIVES
"%{DIM%}:н"
!! __@RULENAME@__
 %{DIM%}:н <=> [ Vows: | :Vows | м  | в | н | д | л | р  | з | й] (ь:)  %> _ я ;
!!€ ши>%{DIM%}я
!!€ ши0ня

"%{DIM%}:к"
!! __@RULENAME@__
 %{DIM%}:к <=> [ ф | т | ц | ч | с | ш | х ] (ь) %> _ я ;
!!€ пикс>%{DIM%}я
!!€ пикс0кя


!!Allow the absence of "ъ" in the first syllable
!"Loss of ъ in the first syllable orthography"
!ъ:0 <= # Cns* _ [ д | з | р | т ] ; REMOVE 2018-09-12

"%{ъØ%}:ъ"
!! __@RULENAME@__
 %{ъØ%}:ъ <=>
 [# | Cns: ] _ \[ # ]* %^Ъ2PED: ;

!! !!Disallow

"Disallow TLoss after non-t"
!! __@RULENAME@__  
TLoss:0 /<= [[ :* Letters ] - [ :* :т ] ] %> _ ;

"Disallow KLoss after non-k"
!! __@RULENAME@__  
KLoss:0 /<= [[ :* Letters ] - [ :* :к ] ] %> _ ;

"Disallow SLoss after non-s"
!! __@RULENAME@__  
SLoss:0 /<= [[ :* Letters ] - [ :* :с ] ] %> _ ;

!"Disallow VLoss after non-U2I2"
!!! __@RULENAME@__  
!VLoss:0 /<= [[ :* [ Letters | А1: | %{АЯ%}: | %{ИЫЙ%}: | К1:  | %{ЬØ%}: | %{гк%}: | %{вгк%}: | К3:  ] ] - [ :* [ У2: | И2: ] ] ] %> _ ;


!"Disallow translative linking vowel vs no linking vowel "
!!! __@RULENAME@__
! k:k /<= [[ :* Letters ] - [ :*  к ]] %> %{ЕОØ%}: _ с  ;
!          к  %>  _ с  ;

"Disallow овок"
!! __@RULENAME@__
в:в /<=
    	[Cns|Vow] [б|в|г|д|ж|з|й:|л|м|н|п|р|с|ф|х|ц|ш|щ] (ь:) %> %{ЕОØ%}: _ о к ;
    	%> [с |т|%{ЕОØ%}: с т|%{ЕОØ%}: ц|%{ЕОØ%}: н|%{ЕОØ%}: н ь к о н ] (ь:) %> %{ЕОØ%}: _ о к ;

"Disallow Онга"
!! __@RULENAME@__
н:н /<=
    	%> [ш к а] %> %{ЕОØ%}: _ г а ;
        %{ЕОØ%}: _ [ к|т ] %> %{гк%}: %{АЕ%}: ;

"Disallow -кс: нетькскс"
!! __@RULENAME@__
к:к /<=
       к с %> _ с ;
       к [Cns - [c]] (ь:) %> %{ЕОØ%}: _ с ;
       [Vows|Cns - [с|ц|ш|ч]] %> %{ЕО%}: %> ш _ а ;
       [с|ц|ш|ч] %> ш _ а ;

"Disallow -гя only after a few"
!! __@RULENAME@__
я:я /<=
     [[:Vows - [:и]]|[Cns - [й]]|[Cns - [л|н|з|р|д]] ь:] %> г _ [#|%>];

"Disallow onset vowel after voiced cons"
!! __@RULENAME@__
%^PXSG1:0 /<=
     [г|й:|к|п|ф|х|ш|щ|ч|[т|с] (ь:) ] _ %>  н е [%>|#] ;
     [Vows:|Vows:|б|в|ж|м|[д|з|л|н|р] (ь:) ] _ %> %{ЕОØ%}: н е [%>|#] ;