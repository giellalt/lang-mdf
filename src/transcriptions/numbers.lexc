!Distributed under the terms of the GNU General Public License version 2
! or any later version.

Multichar_Symbols

+Use/SpellNoSugg

LEXICON Root
HUNDREDSM ;		! 200M
сяда:1 HUNDREDM ;	! 100M
TENSM ;			! 30-99M
TWEENSM ;		! 20-29M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
сяда% :1 HUNDREDT ;	! 100000-100999
TENST ;			! 30000-99999,10000-10999
TWEENST ;		! 21000-29999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
тёжань:1 THOUSAND-PLUS ;	! 1001-1999
тёжан:1 THOUSAND ;	! 1000
UNDERTHOUSAND ;		! 100-999
TENS ;			! 30-99
TWEENS ;		! 20-29 ! not just Hobbits
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
кафта% :2  CUODIM ;
колма% :3 CUODIM ;
ниле% :4 CUODIM ; ! Google: 459
вете% :5 CUODIM ;
кота% :6 CUODIM ;
сисем% :7 CUODIM ;
кафкса% :8 CUODIM ;
вейхкса:9 CUODIM ;

LEXICON CUODIM
сятт% : HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TWEENSM ;
      TEENSM ;
:%0   ONESM ;
:%0%0 MILJON ;

LEXICON TWEENSM
% комсь% :2 TWEENM ;

LEXICON TWEENM
фкие% :1     LOHKAIM ;
кафтува% :2   LOHKAIM ;
колмува% :3    LOHKAIM ;
нилие% :4 LOHKAIM ;
ветие% :5   LOHKAIM ;
котува% :6   LOHKAIM ;
сисемге% :7 LOHKAIM ;
кафксува% :8 LOHKAIM ;
вейхксые% :9   LOHKAIM ;

LEXICON TEENSM
ке:1 TEENM ;

LEXICON TEENM
фкие% :1     LOHKAIM ;
мгафтува% :2   LOHKAIM ;
мголмува% :3    LOHKAIM ;
мнилие% :4 LOHKAIM ;
ветие% :5   LOHKAIM ;
мготува% :6   LOHKAIM ;
мзисемге% :7 LOHKAIM ;
мгафксува% :8 LOHKAIM ;
вейхксые% :9   LOHKAIM ;

LEXICON LOHKAIM
MILJON ;

LEXICON TENSM
кемонь% :1%0 MILJON ;
комсь% :2%0 MILJON ;
колмо:3    LUHKIEM ;
ниле:4 LUHKIEM ;
ведь:5   LUHKIEM ;
код:6   LUHKIEM ;
сизь:7 LUHKIEM ;
кафксо:8 LUHKIEM ;
вейхксо:9   LUHKIEM ;

LEXICON LUHKIEM
гемонь% :%0 MILJON ;
гемонь% :   ONESM ;

LEXICON ONESM
фкя% миллион% :1 OVERTHOUSANDS ;
кафта:2         MILJON ;
колма:3          MILJON ;
ниле:4       MILJON ;
вете:5         MILJON ;
кота:6         MILJON ;
сисем:7       MILJON ;
кафкса:8       MILJON ;
вейхкса:9         MILJON ; 

LEXICON MILJON
% миллион: OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
% сяда% :1 HUNDREDT ;	! 100.000.000
% :%0 TENST ;		!  30.000.000
% :%0 TWEENST ;		!  20.000.000
% :%0 TEENST ;		!  10.000.000
% :%0%0 ONEST ;		!   2.000.000
% тёжань:%0%01 THOUSAND-PLUS ;	!   1.000.000
% :%0%0%0 UNDERTHOUSAND ;	
% :%0%0%0%0 TENS ;
% :%0%0%0%0 TWEENS ;
% :%0%0%0%0 TEENS ;
% :%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	!  x000000

LEXICON HUNDREDST
кафта:2   CUODIT ;	! 200000-299999
колма:3    CUODIT ;	! 300000-399999
ниле:4 CUODIT ;	! 400000-499999
вете:5   CUODIT ;	! 500000-599999
кота:6   CUODIT ;	! 600000-699999
сисем:7 CUODIT ;	! 700000-799999
кафкса:8 CUODIT ;	! 800000-899999
вейхкса:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
% сятт% : HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X3XYYY, X10YYY
TWEENST ;		! X2XYYY
TEENST ;		! X1XYYY
фкя:%01 THOUSANDS ;
:%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TWEENST
комсь% :2   TWEENT ;
комсь:2%0 THOUSANDS ;

LEXICON TWEENT
фкие:1     THOUSANDS ;
кафтува:2   THOUSANDS ;
колмува:3    THOUSANDS ;
нилие:4 THOUSANDS ;
ветие:5   THOUSANDS ;
котува:6   THOUSANDS ;
сисемге:7 THOUSANDS ;
кафксува:8 THOUSANDS ;
вейхксые:9   THOUSANDS ;

LEXICON TEENST
ке:1   TEENT ;
кемонь:1%0 THOUSANDS ;

LEXICON TEENT
фкие:1     THOUSANDS ;
мгафтува:2   THOUSANDS ;
мголмува:3    THOUSANDS ;
мнилие:4 THOUSANDS ;
ветие:5   THOUSANDS ;
мготува:6   THOUSANDS ;
мзисемге:7 THOUSANDS ;
мгафксува:8 THOUSANDS ;
вейхксые:9   THOUSANDS ;

LEXICON TENST
!комсь:2   LUHKIET ;
колмогемонь:3    LUHKIET ;
нилегемонь:4 LUHKIET ;
ведьгемонь:5   LUHKIET ;
кодгемонь:6   LUHKIET ;
сизьгемонь:7 LUHKIET ;
кавксогемонь:8 LUHKIET ;
вейксогемонь:9   LUHKIET ;

LEXICON LUHKIET
:%0 THOUSANDS ;
% фкя:1 THOUSANDS ;
% : ONEST ;

LEXICON ONEST
кафта:2 THOUSANDS ;
колма:3 THOUSANDS ;
ниле:4 THOUSANDS ;
вете:5 THOUSANDS ;
кота:6 THOUSANDS ;
сисем:7 THOUSANDS ;
кафкса:8 THOUSANDS ;
вейхкса:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
% тёжань: THOUSAND-PLUS ;
% тёжань% :%0%0%0 COMMA ;	   ! 1000
% тёжанце% :%0%0%0%+це COMMA ;	   ! 1000
% тёжанце+Use/SpellNoSugg% :%0%0%0 COMMA ;	   ! 1000


LEXICON THOUSAND-PLUS
% : UNDERTHOUSAND ;	   ! 1100-1999
% :%0 TENS ;	   ! 1030-1099
% :%0 TEENS ;	   ! 1010-1019
% :%0 TWEENS ;	   ! 1020-1029
% :%0%0 ONES ;	   ! 1001-1009

LEXICON THOUSAND
ь:%0%0%0 COMMA ;	   !!= @CODE@ 
це% :%0%0%0%-це COMMA ;	   !!= @CODE@ 
це+Use/SpellNoSugg% :%0%0%0 COMMA ;	   !!= @CODE@ Sometimes there is no marking
!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
сяда% :1 HUNDRED ;
сядоце:1  CUODAAT ;

LEXICON HUNDREDS
кафта:2   CUODI ;
колма:3    CUODI ;
ниле:4 CUODI ;
вете:5   CUODI ;
кота:6   CUODI ;
сисем:7   CUODI ;
кафкса:8 CUODI ;
вейхкса:9   CUODI ;

LEXICON CUODI
% сятт% :   HUNDRED ;
% сядоце:  CUODAAT ;


LEXICON HUNDRED
      TENS ;
      TEENS ;
      TWEENS ;
:%0   ONES ;
:%0%0 COMMA ;

LEXICON CUODAAT
:%0%0%-це    COMMA ;

LEXICON TEENS
 CARDINAL-TEENS ;
 ORDINAL-TEENS ;

LEXICON TWEENS
комсь% :2%0 COMMA ;
комсеце:2%0%-це COMMA ;
комсь% :2 CARDINAL-TWEENS ;
комсь% :2 ORDINAL-TWEENS ;



LEXICON TENS
колмо:3 LUHKIE_3-9 ;
ниле:4 LUHKIE_3-9 ;
ведь:5 LUHKIE_3-9 ;
код:6 LUHKIE_3-9 ;
сизь:7 LUHKIE_3-9 ;
кафксо:8 LUHKIE_3-9 ;
вейксо:9 LUHKIE_3-9 ;

LEXICON LUHKIE_3-9
гемонь:%0 COMMA ;
гемонце:%0%-це COMMA ;
гемонь% : ONES ;

!LEXICON LUHKIE_3-9_morph
!кемень+Use/SpellNoSugg:%0 COMMA ;
!кеменце+Use/SpellNoSugg:%0%-це COMMA ;
!кемень+Use/SpellNoSugg% : ONES ;


LEXICON CARDINAL-TEENS
кемонь:1%0 COMMA ;
кефкие:11 COMMA ;
кемгафтува:12 COMMA ;
кемголмува:13 COMMA ;
кемнилие:14 COMMA ;
кеветие:15 COMMA ;
кемготува:16 COMMA ;
кемзисемге:17 COMMA ;
кемгафксува:18 COMMA ;
кевейхксые:19 COMMA ;

LEXICON ORDINAL-TEENS
кемонце:1%0%-це COMMA ;
кефкиеце:11%-це     COMMA ;
кемгавтувоце:12%-це   COMMA ;
кемголмувоце:13%-це   COMMA ;
кемнилиеце:14%-це COMMA ;
кеветиеце:15%-це   COMMA ;
кемготувоце:16%-це   COMMA ;
кемзисемгеце:17%-це    COMMA ;
кемгафксувоце:18%-це   COMMA ;
кевейксыеце:19%-це    COMMA ;

LEXICON CARDINAL-TWEENS
фкие:1 COMMA ;
кафтува:2 COMMA ;
колмува:3 COMMA ;
нилие:4 COMMA ;
ветие:5 COMMA ;
котува:6 COMMA ;
сисемге:7 COMMA ;
кафксуви:8 COMMA ;
вейхксые:9 COMMA ;

LEXICON ORDINAL-TWEENS
фкиеце:1%-це     COMMA ;
кафтувоце:2%-це   COMMA ;
колмувоце:3%-це   COMMA ;
нилиеце:4%-це COMMA ;
ветиеце:5%-це   COMMA ;
котувоце:6%-це   COMMA ;
сисемгеце:7%-це    COMMA ;
кафксувоце:8%-це   COMMA ;
вейхксыеце:9%-це    COMMA ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
фкя:1 COMMA ;
кавта:2 COMMA ;
колма:3 COMMA ;
ниле:4 COMMA ;
вете:5 COMMA ;
кота:6 COMMA ;
сисем:7 COMMA ;
кафкса:8 COMMA ;
вейхкса:9 COMMA ;

LEXICON ORDINAL
васенце:1%-це     COMMA ;
омбоце:2%-це   COMMA ;
колмоце:3%-це   COMMA ;
нилеце:4%-це COMMA ;
ветеце:5%-це   COMMA ;
котоце:6%-це   COMMA ;
сисемце:7%-це    COMMA ;
кафксоце:8%-це   COMMA ;
вейхксэце:9%-це    COMMA ;

LEXICON COMMA
 ENDLEX ;

LEXICON ENDLEX
 # ;
