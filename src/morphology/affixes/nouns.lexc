!! !!!Noun inflection


!! Moksha language nouns inflect in cases.

!!!!!!!!!
!! NOUNS 
!!!!!!!!!

LEXICON NTag
 N_ ;
LEXICON N_
+N+Sg+Nom+Indef+TYÄ: #;

LEXICON N_АВА_DIAL
+Dial+Use/Sub: N_АВА ;
LEXICON N_ОШ_DIAL
+Dial+Use/Sub: N_ОШ ;
LEXICON N_ОФТА_DIAL
+Dial+Use/Sub: N_ОФТА ;
LEXICON N_КАЛ_DIAL
+Dial+Use/Sub: N_КАЛ ;

LEXICON N_АВА
! ава:ава
+N: NMN_АВА ;
+N+Der/Dim+N:%>%{DIM%}я NMN_АВА ;

LEXICON N_АВА-SG
! ава:ава
+N: NMN_АВА-SG ;

LEXICON N_АВА-PL
! ава:ава
+N: NMN_АВА-PL ;

!LEXICON N_АВА-SG/PL
! ава:ава
!+N: NMN_АВА-SG/PL ;


LEXICON N_ОФТА !!= @CODE@ 
!!офта:офт
+N: NMN_ОФТА ;

LEXICON N_ОФТА-SG !!= @CODE@
+N: NMN_ОФТА-SG ;
LEXICON N_ОФТА-PL !!= @CODE@
+N: NMN_ОФТА-PL ;

LEXICON N_ПАНГА !!= @CODE@
!!панга:панг
 N_ОФТА ;

LEXICON N_ПИНГЕ !!= @CODE@
!пинге:пинг
+N:%{АОЕ%} NMN_АВА ;
 N_КАЛ ; !!= @CODE@ Ela, Ill, Ine, Par 
+N+Der/Dim+N:%{АОЕ%}%>%{DIM%}я NMN_АВА ;

LEXICON N_ПУЛА !!= @CODE@
!пула:пул
+N:%{АОЕ%} NMN_АВА ;
+N+Der/Dim+N:%{АОЕ%}%>%{DIM%}я NMN_АВА ;

LEXICON N_ОШ !!= @CODE@
!ош:ош
+N: NMN_ОШ ;
+N+Der/Dim+N:%>%{DIM%}я NMN_АВА ;

LEXICON N_ОШ-PL
!ош:ош
+N: NMN_ОШ-PL ;

LEXICON N_КАЛ !!= @CODE@
 NMN_КАЛ ;
+N+Der/Dim+N:%>%{DIM%}я NMN_АВА ;

LEXICON N_КАЛ-SG
+N: NMN_КАЛ-SG ;

LEXICON N_КАЛ-PL
+N: NMN_КАЛ-PL ;

LEXICON N_КЕЛУ !!= @CODE@
+N: NMN_КАЛ ;

!! NMN

LEXICON NMN_АВА
!	NMN_АВА-SG	;
!	NMN_АВА-PL	;
!	NMN_АВА-SG/PL	;
	NMN-SG	;
	NMN-PL	;
	NMN-SG/PL	;

LEXICON NMN_АВА-SG
 	NMN-SG ;
	NMN-SG/PL	;
LEXICON NMN_АВА-PL
	NMN-PL ;
	NMN-SG/PL	;


LEXICON NMN_ОФТА !!= @CODE@
!!офта:офт
NMN-SG ;
NMN-PL ;
NMN-SG/PL   ;
:%{АОЕ%} NMN-SG ;
:%{АОЕ%} NMN-PL ;
:%{АОЕ%} NMN-SG/PL   ;


LEXICON NMN_ОФТА-SG !!= @CODE@
:%{АОЕ%} NMN-SG ;
	 NMN-SG ;
:%{АОЕ%} NMN-SG/PL   ;
	 NMN-SG/PL   ;
+Der/Dim+N:%{АОЕ%}%>%{DIM%}я NMN_АВА-SG ;

LEXICON NMN_ОФТА-PL !!= @CODE@
:%{АОЕ%} NMN-PL ;
	 NMN-PL ;
:%{АОЕ%} NMN-SG/PL   ;
         NMN-SG/PL   ;
+Der/Dim+N:%{АОЕ%}%>%{DIM%}я NMN_АВА-PL ;

LEXICON NMN_КАЛ
!	NMN_КАЛ-SG	;
!	NMN_КАЛ-PL	;
!	NMN_КАЛ-SG/PL	;
        NMN-SG ;
        NMN-PL ;
        NMN-SG/PL       ;


LEXICON NMN_КАЛ-SG
!! Indefinite
! SG-NOM ;
!! Definite Sg
! SGCXDEF ;
! PXSG1-Sg ;
        NMN-SG ;
        NMN-SG/PL       ;


LEXICON NMN_КАЛ-PL
!! Indefinite
! PL-NOM ;
!! Definite Pl
! PLCXDEF ;
        NMN-PL       ;
        NMN-SG/PL       ;


!LEXICON NMN_КАЛ-SG/PL
! NOTDEF-GEN ;
! NOTDEF-DAT ;
! NOTDEF-ABL ;
! NOTDEF-ILL ;
! NOTDEF-INE ;
! NOTDEF-ELA ;
! NOTDEF-ABE ;
! NOTDEF-TRA ;
! NOTDEF-CMP ;
! NOTDEF-PRL ;

!! Possessor Indices
! PXSG1-Pl ;
! PXSG2 ;
! PXSG3 ;
! PXPL1 ;
! PXPL2 ;
! PXPL3 ;

LEXICON NMN_ОШ
 	NMN_КАЛ		;

LEXICON NMN_ОШ-SG
 	NMN_КАЛ-SG	;

LEXICON NMN_ОШ-PL
 	NMN_КАЛ-PL	;

!LEXICON NMN_ОШ-SG/PL
! 	NMN_КАЛ-SG/PL	;

!! MUTUAL NOMINAL
LEXICON NMN-SG
 SG-NOM ;
!! Definite Sg
 SGCXDEF ;
!! Possessor Indices
 SG-CX-PXSG1 ;

LEXICON NMN-PL
 PL-NOM ;
!! Definite Pl
 PLCXDEF ;
!! Possessor Indices
! PXSG1-Pl ;
 PL-CX-PXSG1 ;

LEXICON NMN-SG/PL
 NOTDEF-GEN ;
 NOTDEF-DAT ;
 NOTDEF-ABL ;
 NOTDEF-ILL ;
 NOTDEF-INE ;
 NOTDEF-ELA ;
 NOTDEF-ABE ;
 NOTDEF-TRA ;
 NOTDEF-CMP ;
 NOTDEF-PRL ;
!! Possessor Indices
 PL-CX-PXSG1 ;
 PXSG2 ;
 PXSG3 ;
 PXPL1 ;
 PXPL2 ;
 PXPL3 ;





LEXICON SGCXDEF
+Sg+Nom+Def:%>сь K ;
+Sg+Gen+Def:%>ть K ;
+Sg+Dat+Def:%>ти K ;


LEXICON PLCXDEF
+Pl+Nom+Def:%>%{Х%}тне K ;
+Pl+Gen+Def:%>%{Х%}тне%>О1нь K ;
+Pl+Dat+Def:%>%{Х%}тне%>О1нди K ;


LEXICON SG-CX-PXSG1
+Sg+Nom+PxSg1:%>О1зе K ;
+Sg+Gen+PxSg1:%>О1зе%>О1нь K ;
+Sg+Dat+PxSg1:%>О1зе%>О1нди K ;
+Sg+Dat+PxSg1:%>О1з%>ти K ;

LEXICON PL-CX-PXSG1
+Pl+Nom+PxSg1:%>О1не K ;
+Pl+Gen+PxSg1:%>О1не%>О1нь K ;
+Pl+Dat+PxSg1:%>О1не%>О1нди K ;

LEXICON PXSG2
+Sg+Nom+PxSg2:%>це K ;
+Sg+Gen+PxSg2:%>це%>О1нь K ;
+Sg+Dat+PxSg2:%>це%>О1нди K ;
+Pl+Nom+PxSg2:%>тне K ;
+Pl+Gen+PxSg2:%>тне%>О1нь K ;
+Pl+Dat+PxSg2:%>тне%>О1нди K ;

LEXICON PXSG3
+Sg+Nom+PxSg3:%>О1ц K ;
+Sg+Gen+PxSg3:%>О1нц K ;
+Sg+Dat+PxSg3:%>О1нцты K ;
+Pl+Nom+PxSg3:%>О1нзО1 K ;
+Pl+Gen+PxSg3:%>О1нзО1%>О1н K ;
+Pl+Dat+PxSg3:%>О1нзО1%>О1нды K ;


LEXICON PXPL1
+Nom+PxPl1:%>О1ньке K ;
+Gen+PxPl1:%>О1ньконь K ;
+Dat+PxPl1:%>О1ньконди K ;

LEXICON PXPL2
+Nom+PxPl2:%>О1нте K ;
+Gen+PxPl2:%>О1нте%>О1нь K ;
+Dat+PxPl2:%>О1нте%>О1нди K ;

LEXICON PXPL3
+Nom+PxPl3:%>снО1 K ;
+Gen+PxPl3:%>снО1%>О1н K ;
+Dat+PxPl3:%>снО1%>О1нды K ;


LEXICON SG-NOM
+Sg+Nom+Indef: K ;

LEXICON NOTDEF-GEN
+SP+Gen+Indef:%>О1нь K ;

LEXICON NOTDEF-DAT
+SP+Dat+Indef:%>О1нди K ;

LEXICON NOTDEF-ABL
+SP+Abl:%>дО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ILL
+SP+Ill+Indef:%>с K ;
+SP+Ill:%>О1з OBLIQUECX-PX-osh ; 

LEXICON NOTDEF-INE
+SP+Ine:%>сО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ELA
+SP+Ela:%>стО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ABE
+SP+Abe:%>фтомО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-TRA
+SP+Tra+Indef:%>кс K ;

LEXICON NOTDEF-CMP
+SP+Comp:%>шка Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-PRL
+SP+Prl:%>Г2а Choose-INDEF-OBLPX-ava ;

!
LEXICON PL-NOM
+Pl+Nom+Indef:%>%{Х%}т K ;
!+Pl+Nom+Indef:%>%{Х%}т%{ЬØ%} K ;



LEXICON Choose-INDEF-OBLPX-kal
+Indef: K ;
 OBLIQUECX-PX-kal ;

LEXICON OBLIQUECX-PX-kal
+PxSg1:%>О1н K ;
+PxSg2:%>т K ;
+PxSg3:%>О1нзО1 K ;
+PxPl1:%>О1нк K ;
+PxPl2:%>О1нт K ;
+PxPl3:%>ст K ;

LEXICON Choose-INDEF-OBLPX-osh
+Indef: K ;
 OBLIQUECX-PX-osh ;

LEXICON OBLIQUECX-PX-osh
+PxSg1:%>О1н K ;
+PxSg2:%>т K ;
+PxSg3:%>О1нзО1 K ;
+PxPl1:%>О1нк K ;
+PxPl2:%>О1нт K ;
+PxPl3:%>О1ст K ;


LEXICON Choose-INDEF-OBLPX-ava
+Indef: K ;
 OBLIQUECX-PX-ava ;

LEXICON OBLIQUECX-PX-ava
+PxSg1:%>н K ;
+PxSg2:%>т K ;
+PxSg3:%>нзО1 K ;
+PxPl1:%>нк K ;
+PxPl2:%>нт K ;
+PxPl3:%>ст K ;


! vim: set ft=xfst-lexc:

