!! !!!Noun inflection


!! Moksha language nouns inflect in cases.

!!!!!!!!!
!! NOUNS 
!!!!!!!!!

LEXICON NTag
 N_ ;
LEXICON N_
+N+Sg+Nom+Indef+TYÄ: #;

LEXICON N_АВА
! ава:ава
+N: NMN_АВА ;

LEXICON N_АВА-SG
! ава:ава
+N: NMN_АВА-SG ;

LEXICON N_АВА-PL
! ава:ава
+N: NMN_АВА-PL ;

LEXICON N_АВА-SG/PL
! ава:ава
+N: NMN_АВА-SG/PL ;

LEXICON NMN_АВА
	NMN_АВА-SG	;
	NMN_АВА-PL	;
	NMN_АВА-SG/PL	;

LEXICON NMN_АВА-SG
 SG-NOM ;
!! Definite Sg
 SGDEFCX ;
!! Possessor Indices
:%> PXSG1-Sg ;

LEXICON NMN_АВА-PL
:%> PL-NOM ;
!! Definite Pl
 PLDEFCX ;
!! Possessor Indices
:%> PXSG1-Pl ;

LEXICON NMN_АВА-SG/PL
:%> NOTDEF-GEN ;
:%> NOTDEF-DAT ;
:%> NOTDEF-ABL ;
:%> NOTDEF-ILL ;
:%> NOTDEF-INE ;
:%> NOTDEF-ELA ;
:%> NOTDEF-ABE ;
:%> NOTDEF-TRA ;
:%> NOTDEF-CMP ;
:%> NOTDEF-PRL ;

:%> PXSG2 ;
:%> PXSG3 ;
:%> PXPL1 ;
:%> PXPL2 ;
:%> PXPL3 ;


LEXICON N_ОФТА
!!офта:офт
:%>О2 N_АВА ;
 N_КАЛ ;

LEXICON N_ПАНГА
!!панга:панг
 N_ОФТА ;

LEXICON N_ПИНГЕ
!пинге:пинг
:%>О2 N_АВА ;
 N_КАЛ ;

LEXICON N_ПУЛА
!пула:пул
:%>О2 N_АВА ;

LEXICON N_ОШ
!ош:ош
+N: NMN_ОШ ;

LEXICON N_ОШ-PL
!ош:ош
+N: NMN_ОШ-PL ;
+N: NMN_ОШ-SG/PL ;

LEXICON N_КАЛ
+N: NMN_КАЛ ;

LEXICON N_КАЛ-PL
+N: NMN_КАЛ-PL ;
+N: NMN_КАЛ-SG/PL ;

LEXICON N-kelu
+N: NMN_КАЛ ;

LEXICON NMN_КАЛ
	NMN_КАЛ-SG	;
	NMN_КАЛ-PL	;
	NMN_КАЛ-SG/PL	;

LEXICON NMN_КАЛ-SG
!! Indefinite
 SG-NOM ;
!! Definite Sg
 SGDEFCX ;
:%>О1 PXSG1-Sg ;

LEXICON NMN_КАЛ-PL
!! Indefinite
:%> PL-NOM ;
!! Definite Pl
 PLDEFCX ;

LEXICON NMN_КАЛ-SG/PL
:%>О1 NOTDEF-GEN ;
:%>О1 NOTDEF-DAT ;
:%> NOTDEF-ABL ;
:%> NOTDEF-ILL ;
:%> NOTDEF-INE ;
:%> NOTDEF-ELA ;
:%> NOTDEF-ABE ;
:%> NOTDEF-TRA ;
:%> NOTDEF-CMP ;
:%> NOTDEF-PRL ;

!! Possessor Indices
:%> PXSG1-Pl ;
:%> PXSG2 ;
:%>О1 PXSG3 ;
:%>О1 PXPL1 ;
:%>О1 PXPL2 ;
:%> PXPL3 ;

LEXICON NMN_ОШ
 	NMN_КАЛ		;

LEXICON NMN_ОШ-SG
 	NMN_КАЛ-SG	;

LEXICON NMN_ОШ-PL
 	NMN_КАЛ-PL	;

LEXICON NMN_ОШ-SG/PL
 	NMN_КАЛ-SG/PL	;




LEXICON SGDEFCX
+Sg+Nom+Def:%>сь K ;
+Sg+Gen+Def:%>ть K ;
+Sg+Dat+Def:%>ти K ;


LEXICON PLDEFCX
+Pl+Nom+Def:%>Х1тне K ;
+Pl+Gen+Def:%>Х1тне%>О1нь K ;
+Pl+Dat+Def:%>Х1тне%>О1нди K ;


LEXICON PXSG1-Sg
+Sg+Nom+PxSg1:зе K ;
+Sg+Gen+PxSg1:зе%>О1нь K ;
+Sg+Dat+PxSg1:зе%>О1нди K ;
+Sg+Dat+PxSg1:з%>ти K ;

LEXICON PXSG1-Pl
+Pl+Nom+PxSg1:не K ;
+Pl+Gen+PxSg1:не%>О1нь K ;
+Pl+Dat+PxSg1:не%>О1нди K ;

LEXICON PXSG2
+Sg+Nom+PxSg2:це K ;
+Sg+Gen+PxSg2:це%>О1нь K ;
+Sg+Dat+PxSg2:це%>О1нди K ;
+Pl+Nom+PxSg2:тне K ;
+Pl+Gen+PxSg2:тне%>О1нь K ;
+Pl+Dat+PxSg2:тне%>О1нди K ;

LEXICON PXSG3
+Sg+Nom+PxSg3:ц K ;
+Sg+Gen+PxSg3:нц K ;
+Sg+Dat+PxSg3:нцты K ;
+Pl+Nom+PxSg3:нзО1 K ;
+Pl+Gen+PxSg3:нзО1%>О1н K ;
+Pl+Dat+PxSg3:нзО1%>О1нды K ;


LEXICON PXPL1
+Sg+Nom+PxPl1:ньке K ;
+Sg+Gen+PxPl1:ньк%>О1нь K ;
+Sg+Dat+PxPl1:ньк%>О1нди K ;
+Pl+Nom+PxPl1:ньке K ;
+Pl+Gen+PxPl1:ньк%>О1нь K ;
+Pl+Dat+PxPl1:ньк%>О1нди K ;

LEXICON PXPL2
+Sg+Nom+PxPl2:нте K ;
+Sg+Gen+PxPl2:нте%>О1нь K ;
+Sg+Dat+PxPl2:нте%>О1нди K ;
+Pl+Nom+PxPl2:нте K ;
+Pl+Gen+PxPl2:нте%>О1нь K ;
+Pl+Dat+PxPl2:нте%>О1нди K ;

LEXICON PXPL3
+Sg+Nom+PxPl3:снО1 K ;
+Sg+Gen+PxPl3:снО1%>О1н K ;
+Sg+Dat+PxPl3:снО1%>О1нды K ;
+Pl+Nom+PxPl3:снО1 K ;
+Pl+Gen+PxPl3:снО1%>О1н K ;
+Pl+Dat+PxPl3:снО1%>О1нды K ;


LEXICON SG-NOM
+Sg+Nom+Indef: K ;

LEXICON NOTDEF-GEN
+Gen+Indef:нь K ;

LEXICON NOTDEF-DAT
+Dat+Indef:нди K ;

LEXICON NOTDEF-ABL
+Abl:дО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ILL
+Ill+Indef:с K ;
+Ill:з OBLIQUECX-PX-osh ; 

LEXICON NOTDEF-INE
+Ine:сО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ELA
+Ela:стО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-ABE
+Abe:фтомО1 Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-TRA
+Tra+Indef:кс K ;

LEXICON NOTDEF-CMP
+Comp:шка Choose-INDEF-OBLPX-ava ;

LEXICON NOTDEF-PRL
+Prl:Г2а Choose-INDEF-OBLPX-ava ;

!
LEXICON PL-NOM
+Pl+Nom+Indef:Х1тЬ2 K ;



LEXICON Choose-INDEF-OBLPX-kal
+Indef: K ;
 OBLIQUECX-PX-kal ;

LEXICON OBLIQUECX-PX-kal
+PxSg1:%>О1н K ;
+PxSg2:%>т K ;
+PxSg3:%>О1нзО1 K ;
+PxPl1:%>О1нк K ;
+PxPl2:%>О1нт K ;
+PxPl3:%>ст K ;

LEXICON Choose-INDEF-OBLPX-osh
+Indef: K ;
 OBLIQUECX-PX-osh ;

LEXICON OBLIQUECX-PX-osh
+PxSg1:%>О1н K ;
+PxSg2:%>т K ;
+PxSg3:%>О1нзО1 K ;
+PxPl1:%>О1нк K ;
+PxPl2:%>О1нт K ;
+PxPl3:%>О1ст K ;


LEXICON Choose-INDEF-OBLPX-ava
+Indef: K ;
 OBLIQUECX-PX-ava ;

LEXICON OBLIQUECX-PX-ava
+PxSg1:%>н K ;
+PxSg2:%>т K ;
+PxSg3:%>нзО1 K ;
+PxPl1:%>нк K ;
+PxPl2:%>нт K ;
+PxPl3:%>ст K ;


! vim: set ft=xfst-lexc:

