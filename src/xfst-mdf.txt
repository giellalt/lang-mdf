! This file is a script for the Xerox xfst interface.
! It gives the morphophonological rules for Nenets.


! In this file, testcases are marked as follows:

! space exclmark euro space upper:lower
! Please add tests for eventual new rules or problems

! To test: in kt/mdf/src, do:
! xfst -e "source xfst-mdf.txt"
! invert net
! save s
! quit
! cat xfst-mdf.txt | grep €|cut -d" " -f3|cut -d":" -f1|lookup s


clear stack


! Definition section !
! ================== !

echo << THE DEFININTIONS >>

define Cns [ б | в | г | д | ж | з 
           | Б | В | Г | Д | Ж | З 
           | й | к | л | м | н | п | р | с | т | ф | х | ц 
           | Й | К | Л | М | Н | П | Р | С | Т | Ф | Х | Ц 
           | ч | ш | щ 
           | Ч | Ш | Щ ] ;

define Vws [ а | е | ё | и | о | у | ы | э | ю | я
           | А | Е | Ё | И | О | У | Ы | Э | Ю | Я ] ;

define Signs [ ъ | ь |  Ъ | Ь ] ;

define Lts [ Cns | Vws | Signs ] ;

define Dummy %> ;



! Rule section !
! ============ !

echo << THE RULES >>


define VowelCorrelation [ А1 -> а || [ а | я ]  %> х _ ,,
                          А1 -> а || [ Cns  %> | %> а ] х _ ,,
                          А1 -> о || [ о | ё ]  %> х _ ,,
                          А1 -> э || [ ы | э | е ]  %> х _ ] ;

define DevoicingX [ Х1 -> х || [ л ] %> _ [ т | н е ] ] ;
 !€ кал>Х1т:калхт

define MorphemeFinalO1Toa [ О1 -> а || _ [ .#. ] ,,
                        О1 -> о ||  _ %> [ О1 з е | О1 н ц | О1 ц | О1 н ц т ы | О1 н з О1 | О1 н к | О1 н т | с т | т | О1 н | О1 н ь | О1 н д ы  ] [ .#. ] ] ;

define MorphemeInitialO1ToZero [ О1 -> 0 || Vws %> _ ,,
                                 О1 -> о || Cns %> _ Cns ] ;


define VowelPalatalization [ а -> я , у -> ю || й %> _ ] ;


define DummyDeletion Dummy -> 0 ;



read regex [

    VowelCorrelation
.o. DevoicingX
.o. MorphemeFinalO1Toa
.o. MorphemeInitialO1ToZero
.o. VowelPalatalization
.o. DummyDeletion
] ;


