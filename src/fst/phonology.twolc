!! # The Moksha morphophonological/twolc rules file 

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-mdf/blob/main/src/fst/phonology.twolc) 

Alphabet !!≈!!The base @CODE@
!! ```
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я !!= @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я !!= @CODE@
!! ```

%{ъØ%}:0	
!! This will represent schwa in first syllable

!! 2016-03-12
%^А2О:0		!!= @CODE@ used with final а in пула 


%{еэ%}:е
%{еэ%}:э

!Suffix final variant vowel for hard vs soft
!Ь2:ь !
Ь2:0 !
 %{ЬØ%}:0	!!= @CODE@ after imperative Sg2
 %{ЬØ%}:ь	!!= @CODE@ after imperative Sg2
! verbal stem consonant that appear in PretI Sg3
!Т1:0 !
 %{ВИУ%}:в       !!= @CODE@ lative
 %{ВИУ%}:и
 %{ВИУ%}:у

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !


! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
А2:я !А2
 %{АЯ%}:я !!= @CODE@ А2 in панемс:панян
!Ы2:и !
Ы2:ы !
 %{ИЫЙ%}:й	!!= @CODE@ phasing out Ы2, eg кундамс:кундай
 %{ИЫЙ%}:ы	!!= @CODE@ phasing out Ы2, eg кандомс:канды
 %{ИЫЙ%}:и	!!= @CODE@ phasing out Ы2, eg панемс:пани

! onset vowel  for morphemes requiring a preceding vowel
 %{ЕОØ%}:е	!!= @CODE@ morpheme onset linking vowel from %^О1
 %{ЕОØ%}:о	!!= @CODE@ morpheme onset linking vowel from %^О1
 %{ЕОØ%}:0	!!= @CODE@ morpheme onset linking vowel from %^О1 also пинге


! derivational suffix protovowel  "mo" and "me"


! пула, офта, пинге
!О2:а
!О2:о
!О2:е
%{АЕØ%}:а	!!= @CODE@  ConNeg with ашень, тят, афоль
%{АЕØ%}:е
%{АЕØ%}:0

%{АО%}:а	!!= @CODE@  пула stem
%{АО%}:о	!!= @CODE@  пула stem
%{АОØ%}:а	!!= @CODE@  офта stem
%{АОØ%}:о	!!= @CODE@  офта stem
%{АОØ%}:0	!!= @CODE@  офта stem

 %{ЕО%}:е	!!= @CODE@ сельме
 %{ЕО%}:о	!!= @CODE@ 

 %{ЕØ%}:е	!!= @CODE@ кече
 %{ЕØ%}:0	!!= @CODE@ 

%{АЕ%}:е
%{АЕ%}:а	!!= @CODE@ prolative

! devoicing
Х1:х
!Х1:0
 %{Х%}:0	!!= @CODE@ This usually precedes the plural marker

%{DIM%}:н		!!= @CODE@ This will be for diminutive initial consonant

! clitic proto consonant
%{гк%}:г !
%{гк%}:к !
%{гк%}:0 !



! Prolative Ga
%{вгк%}:г !
%{вгк%}:к !
%{вгк%}:в !


! Imperative clitic proto consonant
Г4:г !
Г4:к !
Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
К1:к !
!К1:т 
!
%{КТ%}:т	!!= @CODE@ used in imperative and connegative
%{КТ%}:к
! connegative suffix rendering vowel
! панемс+V+Imprt+ScSg2
!!€ пань%>%{Х%}%{КТ%}%{ЬØ%}
!!€ пат0%>0тье0
К3:0 !

! Stem final on Imperative Verbs
!!К4:0 !



!! in ped realized as hard sign

!! ## TRIGGERS
%^RmVow:0 !!= * @CODE@ for removing vowels 
%^Ь2ZERO:0	!!= @CODE@ removes soft sign before vowel or other combination
%^Ъ2PED:0	!!= @CODE@ brings out hard sign, which is
!! otherwise automatically removed %{ъØ%} 
%^Е2О:0		!!= @CODE@ Change stem-final vowel сембе:сембось
%^Е2А:0		!!= @CODE@ Change stem-final vowel мельге:мельганза
%^Я2А:0		!!= @CODE@ Change stem-final vowel рьвя:рьванц
%^А2Е:0		!!= @CODE@ Change stem-final vowel мокша:мокшесь
%^Е2Я:0		!!= @CODE@ Change word-final vowel for dialect тувотне > тувотня

%^Devoice:0	!!= @CODE@ for devoicing изь:исть
%^PXSG1:0		!!= @CODE@ кядне, ярмаконе не vs %{ЕОØ%}не

TLoss:0
SLoss:0
KLoss:0
VLoss:0
WLoss:0
NoVLoss:0

 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <

 %- !!= * `@CODE@` – Hyphen with constructions like "-bdi"
 %> !!= * `@CODE@` – conjugation/declension morpheme boundary suffix
 »  !!= * `@CODE@` – derivation morpheme boundary suffix
 #  !!= * `@CODE@` – word boundary 

;


Sets !!≈ ## The @CODE@

!! ```
Vows = а о у ы э я ё ю и е ;                                            !!= @CODE@
verbStemVows = а о э я ё е ;                                            !!= @CODE@
verbStemVowStrong = а о э я ё е ;                                       !!= @CODE@
noGlideVow = а о у ы э ;                                                !!= @CODE@
glideVow = я ё ю и е ;                                                  !!= @CODE@
BTV = а о у я ё ю ;                                !!= @CODE@ ! Back Trigger Vowels
FTV = ы и е э ;                                   !!= @CODE@ ! Front Trigger Vowels

Cns = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;                       !!= @CODE@
CnsAndSoft = б в г д ж з й к л м н п р с т ф х ц ч ш щ ь ;                       !!= @CODE@
NPC = б в г д ж з к л м н п р с т ф х ц ч ш щ ; ! Nonpalatal Consonants !!= @CODE@
CnsVoiced = б в г д ж з й л м н р ;                                     !!= @CODE@
CnsVoicedDent = д з л н р ;                                             !!= @CODE@
CnsVoicedNonDent = б в г ж й м ;                                        !!= @CODE@
CnsVoiceless = к п с т ф х ц ч ш щ ;                                    !!= @CODE@
CnsVoicelessDent = с т ц ;                                              !!= @CODE@
CnsVoicelessNonDent = к п ф х ч ш щ ;                                   !!= @CODE@
CnsNonDent = б в г ж к м п ф х ч ш щ ;                                  !!= @CODE@
CnsDent = д з л н р с т ц  ;                                            !!= @CODE@
CnsDentNoL = д з н р с т ц  ;                                           !!= @CODE@

Letters = Vows Cns ь ъ ;                                                !!= @CODE@
!! ```

Definitions !!≈ ## The @CODE@

CnsNotCliticVok = [ [Cns - в] | в [ Vws - о ] | в о [ Cns - к] ] ;

! Front Trigger Consonants (less ль)
FTC = [ й: | д ь: | з ь: | н ь: | р ь: | с ь: | т ь: | ц ь: ] ;

! All Consonants
AC = [ FTC | л ь | NPC ] ;

! Left Front Context Soft
LFCS = [ [ FTV AC* [ Cns - CnsDent ] ] | FTV AC* FTC | [ BTV FTC [ AC* [ Cns - CnsDent ] ]* ] ] ;

! Left Front Context Hard
LFCH =[ [ :FTV AC* CnsDent ] | [ BTV FTC [ AC* CnsDent ]+ ] | [ BTV л ь [ к с ] ] ] ;

! Left Back Context
LBC = [ [ BTV ( %> ) NPC:+ ] | [ BTV л ь [[ NPC* NPC ] - [ :* к с ] ] ] ] ;

PossX = [ м | н %{ЬØ%}: | т %{ЬØ%}: | н т %{ЬØ%}:  | н к | с т ] ;

! Suffix-Initial Variable Vowel
SIVV = [ PossX | в | н е н ь | т е н ь | т е т ь  | н т е н ь | л ь | л и | к с | ш к а | с | %{ЕОØ%}: н з %{АО%}: | т ь | н т ь: | н ь: | м А1: | м г а | т с ь | т с т ь | з ь: ] ;


Back =  :* [SubseqBackHarm \[ :FTV | :й | :ь ]* ( л ь ) [NPC]]  ;





Rules !!≈ ## The @CODE@

!!!Vowel changes

!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
!! **@RULENAME@**  
 Vx:Vy <=> [ Cns | Vows ] _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]] ;
!!2013-10-16!!                   Cns  _ К4:0 %>  %{ИЫЙ%}: ;
          where Vx in (а э ы я ё ю)
                Vy in (0 0 0 0 0 0) matched ;
!!цёра+N+Sg+Nom+Ind+Prs+ScSg1: __man/mies__
!!€ цёра%>%{АЯ%}н
!!€ цёр0%>ан

"у:0"
!! **@RULENAME@**
 Vx:Vy <=> :Vows Cns+ _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]] ;
           where Vx in (и у)
                 Vy in (0 0) matched ;

!! тумс+V+Ind+Prs+ScSg1
!!$ #ту%>%{АЯ%}н
!!$ #т0%>ан
!!€ #ту%>%{АЯ%}н
!!€ #ту%>ян
!
!Vowels in Nouns and Adjectives 
!2006-12-02
!"YI3:y"
!И3:ь <=>  CnsDent  _ [%>  [%{гк%}:г|%{гк%}:к|%{вгк%}:г|%{вгк%}:к|с [%> |# ] | с (т) %{АО%}: |:CnsNonDent]| #] ;
!      #   :* л  _ [%> (в:0) :Cns|#] ;
!

! ScSg3 verb
"о:0"
!! **@RULENAME@**  
о:0 <=>  _ %>  %{ИЫЙ%}: ;
[ Cns | Vows ] _  %> [%{АЯ%}:я|%{АЯ%}:а [ н | т ]] ;

! Deverbal noun in ОмА
"A1:o"
!! **@RULENAME@**  
А1:о <=>  [ а |я ] %>  м  _ ;

!!2013-10-16!"%{ИЫЙ%}:yi"
!!2013-10-16!!! __@RULENAME@__  
!!2013-10-16!%{ИЫЙ%}:и <=> [ я:0 | ё:0 | е:0 | ю:0 | ь:0 | й:0 | CnsNonDent (noGlideVow:0)]  %> _ ;

"%{ИЫЙ%}:j"
!! **@RULENAME@**  
%{ИЫЙ%}:й <=> [ я | а | у | и | ю]  %> _ [ # | %- | » | %> в о к | ( %> ) х т  ь | %> [с|д|т] | х т е | н е | т ь ] ;
!!€ стя%>%{ИЫЙ%}%>сь
!!€ стя%>й%>сь
!! мумс+V+Ind+Prt1+ScSg1+OcSg2: **find/löytää**
!!€ му%>%{ИЫЙ%}хтень
!!€ му%>йхтень

"%{ИЫЙ%}:ы"
!! **@RULENAME@**
%{ИЫЙ%}:ы <=> CnsDent (о:) %> _  ;

"%{ИЫЙ%}:и"
!! **@RULENAME@**
%{ИЫЙ%}:и <=> [ CnsNonDent (о:) | CnsDent ь: | е: | й: ] %> _  ;


"%{ИЫЙ%}:0"
!! **@RULENAME@**  
%{ИЫЙ%}:0 <=> [ я | а ]  %> _ [ й: ] ;
!! араламс+V+Der/NomAg+Sg+Nom+PxSg3: **protect/puolustaa**
!!€ арала%>%{ИЫЙ%}й%>%{ЕОØ%}ц
!!€ арала%>0й%>ец

"%{ЕОØ%}:о"
!! **@RULENAME@**
%{ЕОØ%}:о <=> [ б | в | г | к | м | п | ф | х ] (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [ б | в | г | к | м | п | ф | х ] _ %> %{DIM%}: ;
 [[ д | с | т | ц ] (%^PXSG1:)|%> %{Х%}: т %{ЬØ%}:0] %> _  Cns ;
 [з |л|н|р ] (%^PXSG1:) %> _ [Cns - [к|с]] ;
  [з|л|н|р ] %> _ [с ь к |с т ] ;
  Cns _ (%^PXSG1:) %> [Cns | %{ЕОØ%}:] ;
!! район+N+SP+Gen+Indef:
!!€ район%>%{ЕОØ%}нь
!!€ район%>онь
!! ялга+N+Pl+Nom+Indef+Clt/Cop+Prt2+ScPl3
!!€ ялга>{Х}т{ЬØ}>{ЕОØ}льхть
!!€ ялга>0т0>ольхть

"%{ЕОØ%}:е LEFT"
!! **@RULENAME@**
%{ЕОØ%}:е <=
   [ ж | ч | ш | щ ] (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [ д | с | т | ц ] ь: (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [з | л|н|р ] ь: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
 [з |л|н|р ] ь: %> _ [с ь к |с т ] ;
   Cns (ь:) _ # ;
!!€ пинг%{ЕОØ%}#
!!€ пинге#
!!€ эрьхк%{ЕО0%}#
!!€ эрьхке0
   й: %> _ [з %> |з е |з а:|н ь | л е к  | л ь к]  ;
!!€ тий%>%{ЕОØ%}за
!!€ ти0%>еза
!! кой+N+SP+Ill+PxSg1+Clt/Add:
!!€ кой%>%{ЕОØ%}з%>%{ЕОØ%}н%>%{гк%}%{АЕ%}
!!€ ко0%>ез%>он%>га
!!$ кой%>%{ЕОØ%}з%>%{ЕОØ%}н%>%{гк%}%{АЕ%}
!!$ ко0%>0з%>он%>га

"%{ЕОØ%}:е RIGHT"
!! **@RULENAME@**
%{ЕОØ%}:е =>
   [ ж | ч | ш | щ ] (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [ д | с | т | ц ] ь: (%^PXSG1:) %> _  [Cns|%{вгк%}:] ;
 [з | л|н|р ] ь: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
 [з |л|н|р ] ь: %> _ [с ь к |с т ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти0%>еза
   Cns (ь:) _ # ;
!!€ пинг%{ЕОØ%}#
!!€ пинге#
!!€ эрьхк%{ЕО0%}#
!!€ эрьхке0
   й: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
!! вай+N+Sg+Nom+PxSg1 **oil,butter/voi**
!!€ вай%>%{ЕОØ%}зе
!!€ ва0%>езе
   й: %> _ [з %>| н ь | л е к  | л ь к]  ;
!   й: %> _ [Cns - [ к|с|ц|з]] ;

"%{ЕОØ%}:0 LEFT"
!! **@RULENAME@**
%{ЕОØ%}:0 <=  [Vows:|%^Я2А:|%{АО%}:|%{АОØ%}:|%{АЕ%}:|%{АЕØ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:] (%^Е2А:) %> _ Cns ;
!  [Vows|%{АО%}:|%{АОØ%}:|%{АЕ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:] (%^Е2А:) %> _ Cns ;
        [з|й|л|н|р] (ь:) %> _  [к с | с [#| %-|%>]] ;
          Cns (ь:) _ %^RmVow: ;
!! зепе+N+Pl+Nom+Indef: **pocket/tasku**
!!€ зеп%{ЕОØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ зеп00%>0т0

"%{ЕОØ%}:0 RIGHT"
!! **@RULENAME@**
%{ЕОØ%}:0 =>  [Vows:|%^Я2А:|%{АО%}:|%{АОØ%}:|%{АЕ%}:|%{АЕØ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:] (%^Е2А:) %> _ Cns ;
!  [Vows|%{АО%}:|%{АОØ%}:|%{АЕ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:] (%^Е2А:) %> _ Cns ;
        [з|й|л|н|р] (ь:) %> _  [к с | с [#| %-|%>]] ;
          Cns (ь:) _ %^RmVow: ;
   й (%^PXSG1:) %> _ [Cns - [ к|с]] ;
   й %> _ н ц ;
!   й: (%^PXSG1:) %> _ [Cns - [ к|с]] ;
!! вай+N+Sg+Nom+PxSg1 **oil,butter/voi**
!!€ вай%>%{ЕОØ%}зе
!!€ вай%>0зе
!!$ вай%>%{ЕОØ%}нь
!!$ ва0%>0нь
!! вий+N+Sg+Gen+PxSg3: **strength/voima**
!!€ вий>{ЕОØ}нц
!!€ вий>0нц
! корхта%>%{ИЫЙ%}й%>%{ЕОØ%}зе



"%{АОØ%}:о"
!! **@RULENAME@**
%{АОØ%}:о <=>
        Cns _ %> ;
        Cns _ %^PXSG1: %> н ;
        Cns _ %{АОØ%}: %> %{DIM%}: ;
![ б | в | г | к | м | п | ф | х ] %> _  [Cns|%{вгк%}:] ;
! [ д | с | т | ц ] %> _  Cns ;
! [з |л|н|р ] %> _ [Cns - [к|с]] ;
!  [з|л|н|р ] %> _ [с ь к |с т ] ;


"%{АОØ%}:а"
!! **@RULENAME@**
%{АОØ%}:а <=>
   Cns  _ [# | %- ] ;
!!€ панг%{АОØ%}#
!!€ панга#
!   [ ж | ч | ш | щ ] %> _  Cns ;
!   й: %> _ [Cns - [ к|с|ц|з]] ;
! [ д | с | т | ц ] ь: %> _  Cns ;
! [з | л|н|р ] ь: %> _ [Cns - [ к|с]] ;
! [з |л|н|р ] ь: %> _ [с ь к |с т ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти0%>еза

"%{АОØ%}:0"
!! **@RULENAME@**
%{АОØ%}:0 <=>
          Cns: _ %^RmVow: ;
!! офта+N+Sg+Nom+Def **bear/ohto, karhu**
!!€ офт%{АОØ%}%^RmVow%>сь
!!€ офт00%>сь
!! кодама+Pron+Interr+Pl+Nom+Indef:
!!€ кодам%{АОØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ кодап00%>0т0

![Vows|%{АО%}:|%{АЕ%}:|%{ЕО%}:|%{ЕОØ%}:|%{ЕØ%}:]  %> _ Cns ;
!        [з|й|л|н|р] (ь:) %> _  [к с | с [#| %-|%>]] ;

"%{АО%}:о"
!! **@RULENAME@**
%{АО%}:о <=>
        Cns _ %> [%{ЕОØ%}:|%{DIM%}:|%{Х%}: т|Cns] ;
!! пула+N+Sg+Nom+Def: **tail**
!!€ пул%{АО%}%>сь
!!€ пуло%>сь
!!€ пул%{АО%}%>%{ЕОØ%}нз%{АО%}
!!€ пуло%>0нза
!!€ пул%{АО%}%>%{Х%}т%{ЬØ%}
!!€ пуло%>0т0


"%{АО%}:а"
!! **@RULENAME@**
%{АО%}:а <=>
   Cns  _ [ # | %-] ;
!!€ пул%{АО%}#
!!€ пула#

"Vx:0 %{АО%}:0"
!! **@RULENAME@**
Vx:0 <=>
        Cns _ %> %{АЯ%}:  ;
	where Vx in (%{АО%} %{ЕО%}) ;
!! пря+N+SP+Ine+Der/Cop+Prs+ScSg1 **top, head/pää**
!!€ пря%>с%{АО%}%>%{АЯ%}н
!!€ пря%>с0%>ан


"%{ЕО%}:е"
!! **@RULENAME@**
%{ЕО%}:е <=>  _ [#| %- ] ;
!!€ сельм%{ЕО%}#
!!€ сельме#
      [ ш|ч] %> _ %> ш к а ;

"%{ЕО%}:о"
!! **@RULENAME@**
%{ЕО%}:о <=>  [Cns - [ ш|ч|ж]]  _ %> [%{ЕОØ%}:| Cns:|:Cns|%{вгк%}:| %{Х%}:] ;
!!€ сельм%{ЕО%}%>%{ЕОØ%}нь
!!€ сельмо%>0нь
       [с|ц] %> _ %> ш к а ;
  [Cns - [ ш|ч|ж]]  _ %^PXSG1: %> н е ;
!! сельме+N+Pl+Nom+PxSg1: __my eyes/silmäni
!!€ сельм%{ЕО%}%^PXSG1%>не
!!€ сельмо0%>не
      к %> _ %{DIM%}: я ;
!! байдек+N+Der+Der/Dimin+N+Sg+Nom+Indef: **staff/sauva**
!!€ байдек%>%{ЕО%}%{DIM%}я
!!€ байдек%>оня

"%{ЕØ%}:е"
!! **@RULENAME@**
%{ЕØ%}:е  <=>  _ [%> |# | %- ] ; 

"%{ЕØ%}:0"
!! **@RULENAME@**
%{ЕØ%}:0  <=>  _ %^RmVow: ;
!! тяште+N+Pl+Nom+Def **star/tähti**
!!€ тяшть%{ЕØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ тяшт000%>0ть


"0:j"
!! **@RULENAME@**  
 0:й <=> %> [ %{ИЫЙ%}:ы | %{ИЫЙ%}:и ] %> _  х т [ :ь ]  ;
!CHECKME 2020-10-8

"е:0"
!! **@RULENAME@**
 е:0 <=>  _  %> %{ИЫЙ%}: [ # | %- | %> [в о к|%{гк%}: %{АЕ%}:] | (%>)  х т  :ь |н | х т е|т]  ;
          _  %> %{ИЫЙ%}: [ й: %> %{Х%}: ]  ;
         [ Cns | Vows ] _  [ %> %{АЯ%}:я| %> %{АЯ%}:а [ н | т ]| (%^Ь2ZERO:) %> %{ВИУ%}:] ; 
!! веле+N+SP+Lat+Indef
!!€ веле%>%{ВИУ%}
!!€ вел0%>и
!!!!!

"е:о"
!! **@RULENAME@**
 е:о <=> Cns: _ %^Е2О: %> (%{АО%}:|%{Х%}:) Cns: ;

! CHECKME
"е:а"
!! **@RULENAME@**
 е:а <=> Cns: _ %^Е2А: %> (%{АО%}:|%{Х%}:|%{ЕОØ%}:) Cns: ;

"е:я"
!! **@RULENAME@**
 е:я <=> Cns: _ %^Е2Я: ;


!! тувотня:тува+N+Pl+Nom+Def+Err/Orth-lowered-final-e-2-ja    !!≈ * @CODE@ final е lowered to я
!!€ тув%{АО%}%>%{Х%}тне%^Е2Я
!!€ туво%>0тня0

"%{АЕ%}:я"
!! **@RULENAME@**
 %{АЕ%}:я <=> :Cns _ %^Е2Я: ;


"%{ЕОØ%}:я"
!! **@RULENAME@**
 %{ЕОØ%}:я <=> _ %^Е2Я: ;

"%{ЕО%}:я"
!! **@RULENAME@**
 %{ЕО%}:я <=>  _ %^Е2Я: ;

"%{ЕØ%}:я"
!! **@RULENAME@**
 %{ЕØ%}:я <=>  _ %^Е2Я: ;


"а:е"
!! **@RULENAME@**
 а:е <=> Cns: _ %^А2Е: %> ;

"а:о"
!! **@RULENAME@**
 а:о <=> Cns: _ %^А2О: %>  ;

"я:а"
!! **@RULENAME@**
 я:а <=>
 [CnsNonDent:|%{DIM%}:к] _ %^Я2А: %> [%{ЕОØ%}: [(н) ц| н т|н з|з %>| (н) к с] |%{вгк%}: %{АЕ%}:| %{Х%}: т %{ЬØ%}: [#|%-|%>]|ф т о м| д %{АО%}: |ш к а|с  [#|%-|%>]|с (т) %{АО%}: |%{ВИУ%}:|с н %{АО%}:] ;
! Cns: _ %^Я2А: %> ;
!!рьвя+N+Sg+Gen+PxSg3: __wife/vaimo__
!!€ рьвя%^Я2А%>%{ЕОØ%}нц
!!€ рьва0%>0нц
!!€ ърьвя%^Я2А%>%{Х%}т%{ЬØ%}
!!€ ърьва0%>0т0
!!€ ава%>з%>%{ЕОØ%}нт
!!€ ава0з0онт
!! вальмя+N+Pl+Nom+Indef: **window/ikkuna**
!!€ вальмя%^Я2А%>%{Х%}т%{ЬØ%}
!!€ вальма0%>0т0


"%{АЕØ%}:а Always"
!! **@RULENAME@**  
 %{АЕØ%}:а <= BTV [Cns - [ ш | ч | ж | щ | й |в|д | л | з]]+ %> _ [ # | %- ]  ;
 ![Cns - [ ш | ч | ж | щ | й |в|д | л | з]] %> _ [ # | %- ]  ;
 	      Cns [д | л | з]  %> _  [ # | %- ] ;
              BTV [Cns - й]* [ ш | ч | ж | щ ] _ [ # | %- ]  ;
!              BTV [б|в|г|ж|к|м|п|ф|х|ч|ш|щ]* [ ш | ч | ж | щ ] _ [ # | %- ]  ;
!              [ ш | ч | ж | щ ] _ [ # | %- ]  ;

"%{АЕØ%}:а Sometimes"
!! **@RULENAME@**  
 %{АЕØ%}:а => BTV [Cns - [ ш | ч | ж | щ | й |в|д | л | з]]+ %> _ [ # | %- ]  ;
! [Cns - [ ш | ч | ж | щ | й |в|д | л | з]] %> _ [ # | %- ]  ;
              [д | л | з]  %> _  [ # | %- ] ;
              BTV [Cns - й]* [ ш | ч | ж | щ ] _ [ # | %- ]  ;
!              BTV [б|в|г|ж|к|м|п|ф|х|ч|ш|щ]* [ ш | ч | ж | щ ] _ [ # | %- ]  ;
!              [ ш | ч | ж | щ ] _ [ # | %- ]  ;

"%{АЕØ%}:е Always"
!! **@RULENAME@**  
 %{АЕØ%}:е <= [ ш | ч | ж | щ | й: |[Cns - [ д | л | з]] ь:] %> _  [ # | %- ] ;
 	      Cns [д | л | з] ь: %> _  [ # | %- ] ;
              [ ш | ч | ж | щ ] _ [ %> (%{Х%}:) Cns | %> [%{ЕОØ%}:|%{DIM%}:] ]  ;
	       [ь:|й|FTV (н)] [б|г|ж|к|м|п|ф|х|ч|ш|щ]+ %> _  [ # | %- ] ;
"%{АЕØ%}:е Sometimes"
!! **@RULENAME@**  
 %{АЕØ%}:е => [ ш | ч | ж | щ | й: |[Cns - [ д | л | з]] ь:] %> _  [ # | %- ] ;
              [д | л | з] ь: %> _  [ # | %- ] ;
              [ ш | ч | ж | щ ] _ [ %> (%{Х%}:) Cns | %> [%{ЕОØ%}:|%{DIM%}:] ]  ;
	       [ь:|й|FTV (н)] [б|г|ж|к|м|п|ф|х|ч|ш|щ]+ %> _  [ # | %- ] ;
!! мокша+N+Sg+Nom+Def: **Moksha**
!!€ мокш%{АЕØ%}%>сь
!!€ мокше%>сь

"%{АЕØ%}:0 Always"
!! **@RULENAME@**  
 %{АЕØ%}:0 <=
    [ Vows | в ]  %> _ [ # | %- ]  ;
    _ %> %{ВИУ%}: ;

"%{АЕØ%}:0 Sometimes"
!! **@RULENAME@**  
 %{АЕØ%}:0 =>
    Vows ([ в | д | л | з] (ь:)) %> _ [ # | %- ]  ;
    _ %> %{ВИУ%}: ;


"%{АЕ%}:a"
!! **@RULENAME@**  
 %{АЕ%}:а <=>
   [ [Vows - [и]] |# к и | NPC |ь| %{ЬØ%}:0| ъ |:Vows - [:и]] (%>) [%{вгк%}:|%{гк%}:|г]  _  (%^Е2А:) %>  ;
   [ [Vows - [и]] | NPC | %{ЬØ%}:0| ъ |:Vows - [:и]] (%>) [%{вгк%}:|%{гк%}:| [:0]* н г]  _ [ # | %- ] ;
!! вал+Hom1+N+Sg+Nom+Indef+Clt/Add: **word/sana**
!!€ вал%>%{гк%}%{АЕ%}#
!!€ вал%>га#
!! мельге+Pron+Prl+PxSg3: **after**
!!€ мельг%{АЕ%}%>%{ЕОØ%}нз%{АО%}
!!€ мельга%>0нза
!!$ мельг%{АЕ%}%>%{ЕОØ%}нз%{АО%}
!!$ мельге%>0нза
  [:Vows - [:и]] (%^Я2А:) %> %{вгк%}: _ [ # | %- | %> ] ;
!! вальмя+N+SP+Prl+Indef: **window/ikkuna**
!!€ вальмя%^Я2А%>%{вгк%}%{АЕ%}
!!€ вальма0%>ва

   ь ф к _ %>  ;
!перьфке



"%{АЕ%}:е"
!! **@RULENAME@**  
 %{АЕ%}:е <=> [ :ь| й] (%>) [%{вгк%}:|%{гк%}:|г] _ [ # | %- ] ;
!! менель+N+SP+Prl+Indef **sky/taivas**
!!€ менель%>%{вгк%}%{АЕ%}
!!€ менель%>ге
!!€ шяй%>%{вгк%}%{АЕ%}
!!€ шяй%>ге
     и %> [%{вгк%}:|%{гк%}:|н г|г] _ [ # | %- ] ;
!! аля+N+Sg+Dat+PxSg1+Foc **father/isä**
!!€ аля%>%{ЕОØ%}з%>ти%>%{гк%}%{АЕ%}
!!€ аля%>0з%>ти%>ге
   [ч|ш|ж|ч] _ %> ;
   ь ф к _ [# | %- ] ;
!перьфке

"%{АЯ%}:ya"
!! **@RULENAME@**
%{АЯ%}:я <=>     [ CnsDent: [ (Vow) я:0 | е:0 | ь: ]| й: | # Cns* [у|и]] %>  _  [ т | н ] ;
!!€ пань>%{АЯ%}н
!!€ пан00ян
!! тумс+V+Ind+Prs+ScSg1 **leave/lähteä**
!!€ ту>%{АЯ%}н
!!€ ту>ян

   [ CnsDentNoL ь: [ г: ] | ь: | е | й ] %> %{Х%}: т _ [ д | м | н ] ;
!!€ пань>%{Х%}т%{АЯ%}ма
!!€ пан000тяма
!! мярьгомс+V+Ind+Prs+ScPl1: **say/sanoa**
!!€ мярьг%>%{Х%}т%{АЯ%}ма
!!€ мярьк%>0тяма
   [ CnsDentNoL ь: [ г: ] | ь: | е | й | # \[Vows:|%>|#]* [и|я|е|э] [м:|ж:|в: ]] %> %{Х%}: %{КТ%}: _ [ # | %- | к а] ;
!!€ сим%>%{Х%}%{КТ%}%{АЯ%}
!!€ сип%>0тя
   # \[Vows:|%>|#]* [и|я|е|э] [м:|ж:|в: ] %> %{Х%}: т _ [ д | м | н ] ;
!! симомс+V+Ind+Prs+ScPl1: **say/sanoa**
!!€ сим%>%{Х%}т%{АЯ%}ма
!!€ сип%>0тяма


"%{АЯ%}:a"
!! **@RULENAME@**  
%{АЯ%}:а <=> [:BTV | CnsDent: ] %>  %{Х%}: т  _  [ д | н | м ] ;
 [ а| о| у | ё| ю ] \[Vows:|%>|#]+ я %>  %{Х%}: т  _  [ д | н | м ] ;
 [ [ [ а| о| у | ё| ю ] ( %> ) NPC:+ ] | [ BTV л ь [[ NPC* NPC ] - [ :* к с ] ] ] ] %>  %{Х%}: т  _  [ д | н | м ] ;
![ :BTV | LBC | CnsDent: ] %>  %{Х%}: т  _  [ д | н | м ] ;

   [:BTV | CnsDent: | [ а| о| у | ё| ю ] \[Vows:|%>|#]+ я| [ [ [ а| о| у | ё| ю ] ( %> ) NPC:+ ] | [ BTV л ь [[ NPC* NPC ] - [ :* к с ] ] ] ] ] %> %{Х%}: %{КТ%}: _ [ # | %- | к а] ;
    [[ CnsDent | CnsNonDent ] ( а: | о: )| %{ЕО%}:|%{АЕ%}:|%{ЕОØ%}:| %{АОØ%}:| %{АО%}: ] %>  _  [ т | н ] ;
!    [[ CnsDent | CnsNonDent ] ( а:0 | о:0 )| %{ЕО%}:|%{АЕ%}:|%{ЕОØ%}:| %{АОØ%}:| %{АО%}: ] %>  _  [ т | н ] ;
!!€ канд>%{АЯ%}н
!!€ канд0ан
  щ е: %> _ [ т | н ] ;
!!€ аще%>%{АЯ%}н
!!€ ащ0%>ан



"j:0 LEFT"
!! **@RULENAME@**  
й:0 <= [ :Vows| Vows %> %{ИЫЙ%}:] _ [ %> %{ВИУ%}: | %>  %{АЕØ%}:| %>  %{ЕОØ%}:е | %> %{ИЫЙ%}:и|   %> [%{АЯ%}:я | %{АО%}: [ :Cns | К3:0 | # | %- ]] ] ;
!!€ тий>{ЕОØ}за
!!€ ти0>еза
 Vows CnsAndSoft+  [и|ы] _ %> %{Х%}: т: ;
 т и [й: %> %{ИЫЙ%}: |и] _ %> %{Х%}: т: ;
!! седи+N+Pl+Nom+Indef **heart/sydän**
!!€ седий%>%{Х%}т%{ЬØ%}
!!€ седи0%>хть
 [ е: |ь: | Cns |Vows ] %> %{ИЫЙ%}: _ %> %{Х%}:  ;
! [ е: |ь: | CnsNonDent |Vows ] %> %{ИЫЙ%}: _ %> %{Х%}:  ;
!! корхнемс+V+NomAg+Pl+Nom+Def: **visit, talk/jutella**
!!€ корхне%>%{ИЫЙ%}й%>%{Х%}тне
!!€ корхн0%>и0%>х0не
!! озондомс+V+NomAg+Pl+Nom+Def: **pray/rukoilla**
!!€ озонд%>%{ИЫЙ%}й%>%{Х%}тне
!!€ озонд%>ы0%>х0не
!!€ тонафт>{ИЫЙ}й>{ЕОØ}нь
!!€ тонафт>ы0>ень
!!$ тонафт>{ИЫЙ}й>{ЕОØ}нь
!!$ тонафт>ый>ень

"j:0 RIGHT"
!! **@RULENAME@**  
й:0 => [ :Vows| Vows %> %{ИЫЙ%}:] _ [ %> %{ВИУ%}:| %> %{ЕОØ%}:е | %>  %{АЕØ%}:| %> %{ИЫЙ%}:и|   %> [%{АЯ%}:я | %{АО%}: [ :Cns | К3:0 | # | %- ]] ] ;
!!€ тий%>%{ЕОØ%}за
!!€ ти00еза

 Vows CnsAndSoft+  [и|ы] _ %> %{Х%}: т: ;
 т и [й: %> %{ИЫЙ%}: |и] _ %> %{Х%}: т: ;
!! седи+N+Pl+Nom+Indef **heart/sydän**
!!€ седий%>%{Х%}т%{ЬØ%}
!!€ седи0%>хть
 [ е: |ь: | Cns |Vows ] %> %{ИЫЙ%}: _ %> %{Х%}:  ;
! [ е: |ь: | CnsNonDent |Vows ] %> %{ИЫЙ%}: _ %> %{Х%}:  ;
 %{ИЫЙ%}: _ %> %{ЕОØ%}: ;
!! араламс+V+Der/NomAg+Sg+Nom+PxSg3: **protect/puolustaa**
!!€ арала%>%{ИЫЙ%}й%>%{ЕОØ%}ц
!!€ арала%>00%>ец

!"й:х"
!! **@RULENAME@**
!й:х <=>
    _ %> %{Х%}: т %{АЯ%}: [ м|д] а ;
! CHECKME 2017-03-07


!
!18.04.2007 Adding "U2:" and contemplating environment "%> т н _"
!
"ye:e always"
!! **@RULENAME@**  
%{еэ%}:э <=
          CnsDent (%{АО%}:0) %>  н _ [[ # | %-|%> %{гк%}:0 а:я]| н ь: | к] ;
!          [[[Back - [:* Vows]] %>| CnsDent]  %> ] т: н _ [ # | %- |%> [%{гк%}:0 а:я |т| л|%{вгк%}:в а | в | д | к | н | с | ш ]];


"ye:e sometimes"
!! **@RULENAME@**  
%{еэ%}:э =>
          [CnsDent|CnsNonDent] ( %{АО%}:0 ) %>  н _ [[ # | %-|%> %{гк%}:0 а:я]|н ь:|к ] ;
!          [ CnsDent | CnsNonDent | м:0 ]  ( %{АО%}:0 | %{АОØ%}:0 ) %> ( VLoss:0 ) ( TLoss:0 ) т: н _ [ # | %- | %> [%{гк%}:0 а:я | т | л | %{вгк%}:в а | в | д | к | н | с | ш ]];


"a:ya"
!! **@RULENAME@**  
а:я  <=> :Vows ((%>) К3:0 )  %> %{гк%}:0 _  ;

"о:а"
!! **@RULENAME@**  
о:а  <=> Cns _ %> %{АЕØ%}: [ # | %- ]  ;
!! удомс+V+Ind+ConNeg
!!€ удо%>%{АЕØ%}#
!!€ уда%>0#

!! ### CONSONANT CHANGES
" %{Х%}:0 after vowel"
!! **@RULENAME@**  
%{Х%}:0 <=> [ :Vows (%^Е2О:|%^А2О:|%^Я2А:) |з:|ж:| т ( ь: ) | к |г:| с | ш | ц | м:|в: | п | ч | х | щ | ф | н: ( ь: ) | д: ( ь: ) | г:к ] %> _ ;
	 [ з:|ж:| т ( ь: ) | к |г:| с | ш | ц | п | ч | х | щ | ф | м: |в: | н: ( ь: ) | д: ( ь: ) | г:к ] [%{ЕОØ%}:|%{ЕØ%}:|%{АОØ%}:] %^RmVow: %> _ ;
!! валда+N+Pl+Nom+Def **light/valo**
!!€ валд%{АОØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ валт00%>0т0
        %{АО%}: %> _ ;
!!€ пул%{АО%}%>%{Х%}т%{ЬØ%}
!!€ пуло%>0т0

" %{Х%}:х after some consonants"
!! **@RULENAME@**  
%{Х%}:х <=> [ р | л | й: ] ( ь: )  %> _ ;
!! ваны+N+Pl+Nom+Def **watcher/katsoja**
!!€ ваный%>%{Х%}тне
!!€ ваны0%>х0не

"м:п ям"
!! **@RULENAME@**  
м:п <=> _  ( ь: | %{АОØ%}: (%^RmVow:)) %> %{Х%}: [ т: | %{КТ%}: ]  ;
!!€ ям%>%{Х%}т%{ЬØ%}
!!€ яп%>0т0

"в:ф кев"
!! **@RULENAME@**  
в:ф <=> _  ( ь: ) %> %{Х%}: [ т: | %{КТ%}: ]  ;
!!€ кев%>%{Х%}т%{ЬØ%}
!!€ кеф%>0т0

"б:п сруб"
!! **@RULENAME@**  
б:п <=> _  ( ь: ) %> %{Х%}: [ т: | %{КТ%}: ]  ;
!!€ сруб%>%{Х%}т%{ЬØ%}
!!€ сруп%>0т0

"н:т сан"
!! **@RULENAME@**  
н:т <=> _  ( ь: ) %> %{Х%}:  т:   ;
       _   ( ь: ) %> %{Х%}:  %{КТ%}:   ;
!! сан+N+Pl+Nom+Indef: **sinew/suoni**
!!€ сан%>%{Х%}т{ЬØ}
!!€ сат%>0т0
!! ваномс+V+Imprt+ScSg2: **look/katsoa**
!!€ ван%>%{Х%}%{КТ%}{ЬØ}
!!€ ват%>0т0
!! панемс+V+Imprt+ScSg2: **put/laittaa**
!!€ пань%>%{Х%}%{КТ%}{ЬØ}
!!€ пат0%>0ть

!2016.QQ
!Ablative d/t
"d:t"
!! **@RULENAME@**  
д:т  <=> [ CnsVoiceless | д: | г] (ь:) (%{ЕОØ%}: %^RmVow:|%{АОØ%}: %^RmVow:)  %> _ [ %{АО%}: ] ;
!__д:т од:оттне definite plural__
          _ (ь:)  [([%{ЕОØ%}:|%{ЕØ%}:|%{АОØ%}:] %^RmVow:) %> [ %{Х%}: [ т: н е: | т я | т %{АЯ%}: | т %{ЬØ%}:  | %{КТ%}: ]| к | ф]| %^Devoice: ] ;
!          _ (ь:)  [([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> [ %{Х%}: [ т: н е: | т я | т %{АЯ%}: | т %{ЬØ%}: ] | %{КТ%}: | к | ф]| %^Devoice: | %> д: %{АО%}: ] ;
!! кулхцондомс+V+Imprt+ScSg2: **listen**
!!€ кулхцонд>{Х}{КТ}{ЬØ}
!!€ кулхцонт>0т0
!! офта+N+SP+Abl+Indef
!!€ офт%{АОØ%}%^RmVow%>д%{АО%}
!!€ офт00%>та
!!$ офт%{АОØ%}%^RmVow%>д%{АО%}
!!$ офт00%>да
!! кядь+N+SP+Abl+Indef: __hand;arm/käsi
!!€ кядь%>д%{АО%}
!!€ кядь%>та
!!$ кядь%>д%{АО%}
!!$ кять%>та
!! кандомс+V+Ind+Prs+ScSg1+OcSg2
!!€ канд%>%{Х%}тя
!!€ кант%>0тя
!!€ канд>%{Х%}%{КТ%}%{ЬØ%}
!!€ кант%>0т0
!!€ лемдь%>%^Devoice%>ф
!!€ лемть%>0%>ф
!!€ пильг%{ЕОØ%}%^RmVow%>д%{АО%}
!!€ пильг00%>та


"З:С"
!! **@RULENAME@**
 з:с <=> Vows _ (ь:) [%^Devoice: %^Ь2ZERO: |([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> %{Х%}:] ;
!! изь+V+Aux+Neg+Ind+Prt1+ScPl3:	**they did not**
!!€ изь%^Devoice%^Ь2ZERO%>ть
!!€ ис000%>ть

  Vows _ (ь:) %> %{DIM%}: ;
!!€ келазь%>%{DIM%}я

"Ж:Ш"
!! **@RULENAME@**
 ж:ш <=> Vows _ (ь:) [%^Devoice:|([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> %{Х%}:] ;

"ж:ч панчсь"
!! **@RULENAME@**
ж:ч <=> н _  %> [ с | т | ф  | %{Х%}:]  ;
!!€ панж%>сь
!!€ панч%>сь


"г:к"
!! **@RULENAME@**
г:к <=>
!"д:т од:оттне definite plural"
          _ ([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:)  %> [ %{Х%}: [ т: н е: | т я | т %{АЯ%}: | т %{ЬØ%}:| %{КТ%}: ] | %{КТ%}: | к | ф ];
!!€ мярьг%>%{Х%}т%{АЯ%}ма
!!€ мярьк%>0тяма
! WHAT IS THIS      CnsVoiceless (ь) %> _ %{АЕØ%}: [# | %- ] ;



"с:ц ломань:ломанць"
!! **@RULENAME@**  
с:ц <=> н ( ь: ) %> _ [ (т) ь | а | ы | ( т ) %{АО%}: | н | %> | # | %- ] ;

!Changes in suffix consonants

!!!Depricated due to 
!!ERROR: Label not found in 'EQV_LABELS' (114) **
!!Solved in several contexts by declaring both lexical
!!and surface forms in the alphabet.
"G1:0"
!! **@RULENAME@**  
%{гк%}:0 <=> [ [:Vows - [:и]] ((%>) К3:0 )  %> | К1: %{ЬØ%}:0 %> KLoss:0 | :к %> KLoss:0 ] _ а: ;


"G1:g"
!! **@RULENAME@**  
%{гк%}:г <=> [CnsVoicedDent (:ь|%{ЬØ%}:0)| й | и | [ CnsVoicedNonDent - г ] ( :ь )] %> _ [ а|%{АЕ%}:] ;
!! вал+Hom1+N+Sg+Nom+Indef+Foc **word/sana**
!!€ вал%>%{гк%}%{АЕ%}
!!€ вал%>га
!! аля+N+Sg+Dat+PxSg2+Foc **father/isä**
!!€ аля%>цти%>%{гк%}%{АЕ%}
!!€ аля%>цти%>ге

"G1:k"
!! **@RULENAME@**  
%{гк%}:к <=>  [ CnsVoicelessDent ( :ь | %{ЬØ%}:0 | %{ЬØ%}:ь ) | CnsVoicelessNonDent | г | т %{ЬØ%}: | К1: %{ЬØ%}: | К4:0 ] :0* %> _ [а|%{АЕ%}:]  ;
!! вал+Hom1+N+SP+Ill+Indef+Foc **word/sana**
!!€ вал%>с%>%{гк%}%{АЕ%}
!!€ вал%>с%>ка
!! офто+N+SP+Abl+PxPl2+Foc
!!€ офт%{АОØ%}%>д%{АО%}%>%{ЕОØ%}нт%>%{гк%}%{АЕ%}
!!€ офто%>до%>0нт%>ка
!! ава+N+SP+Ela+PxPl2+Foc
!!€ тядя%>ст%{АО%}%>%{ЕОØ%}нт%>%{гк%}%{АЕ%}
!!€ тядя%>сто%>0нт%>ка

!Lative
"%{ВИУ%}:v"
!! **@RULENAME@**
 %{ВИУ%}:в <=> [а | я] %> _ ;

!Lative
"%{ВИУ%}:i"
!! **@RULENAME@**
 %{ВИУ%}:и <=> [е: | ь: | и:] (%^Ь2ZERO:) %> _ ;
                Vow й: %> _ ;
!! учительскай+N+SP+Lat+Indef: **teachers' lounge/opettajain huone**
!!€ учительскай%>%{ВИУ%}
!!€ учительска0%>и
         [FTV - [ы|и]| ь: ] NPC* CnsNonDent %> _ ;
!! пирьф+N+SP+Lat+Indef: **garden/tarha**
!!€ пирьф%>%{ВИУ%}
!!€ пирьф%>и


!Lative
"%{ВИУ%}:u"
!! **@RULENAME@**
 %{ВИУ%}:у <=> [[BTV NPC* CnsNonDent | CnsDent] (%{АОØ%}: %^RmVow: )|%{АЕØ%}:] %> _ ;
! [NPC (%{АОØ%}: %^RmVow: )|%{АЕØ%}:] %> _ ;
!! уша+N+SP+Lat+Indef: **outdoors**
!!€ уш%{АЕØ%}%>%{ВИУ%}
!!€ уше%>у
   [ы|и] NPC* CnsNonDent %> _ ;
!!€ Крым%>%{ВИУ%}
!!€ Крым%>у

!Prolative
"G2:g"
!! **@RULENAME@**  
%{вгк%}:г <=> [ CnsVoicedDent ( :ь )|и| [ CnsVoicedNonDent - г ] ( :ь ) ] %> _ %{АЕ%}: ;

"G2:v"! 
%{вгк%}:в <=>  [:Vows - :и] (%^Я2А:) %>  _ %{АЕ%}: ;
!! вальмя+N+SP+Prl+Indef
!!€ вальмя%^Я2А%>%{вгк%}%{АЕ%}
!!€ вальма0%>ва


"G2:k"
!! **@RULENAME@**  
%{вгк%}:к <=>  [ CnsVoicelessDent ( :ь )| CnsVoicelessNonDent ( :ь )| г ] %> _ %{АЕ%}: ;

! 2012-08-07
"G4:g" !Imperative Clitics
Г4:г  <=> з ь %> _ а ;



"G4:0"
!! **@RULENAME@**  
Г4:0  <=>
           [ К1: %{ЬØ%}: | к ] %> _ а ;
  [%{АО%}: ] %> _ а: ;

"G4:k"
!! **@RULENAME@**  
Г4:к  <=>  # [и л я|у ж о|а з ё|а д я|а й д а] %> _ а  ;


"m:0"
!! **@RULENAME@**  
м:0 <=> 
 BTV  _ %{АОØ%}:0  %>  %^RmVow: [ т| т н е: ] ;



!2016.QQ
!! ### imperative suffix
"К1 %{КТ%}:t left"
!! **@RULENAME@**  
 %{КТ%}:т <= [ Vow н: | CnsVoiceless |CnsVoiced: - г:] ( ь: ) %> (%{Х%}:) _  ;
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть

"К1 %{КТ%}:t right"
!! **@RULENAME@**  
 %{КТ%}:т =>  [ н: | CnsVoiceless |CnsVoiced: - г:| д: ] ( ь: ) %> (%{Х%}:) _  ;
!!€ канд>%{Х%}%{КТ%}%{ЬØ%}
!!€ кант>00т0
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть

!2016.QQ
"К1 %{КТ%}:к left"
!! **@RULENAME@**  
 %{КТ%}:к <=  [ Vows ] %> (%{Х%}:) _  ;
! %> %{Х%}: [ т | %{КТ%}: ] 
!!€ кунда>%{Х%}%{КТ%}%{ЬØ%}
!!€ кунда00к0

"К1 %{КТ%}:к right"
!! **@RULENAME@**  
 %{КТ%}:к =>  [ Vows | д: ( ь: ) ] %> (%{Х%}:) _  ;
! %> %{Х%}: [ т | %{КТ%}: ] 
!!€ кунда>%{Х%}%{КТ%}%{ЬØ%}
!!€ кунда00к0

"К1 %{КТ%}:0 "
!! **@RULENAME@**
 %{КТ%}:0 <=>  г: %> (%{Х%}:) _  ;
!! кельгомс+V+ConNegII:
!!€ кельг>%{Х%}%{КТ%}%{ЬØ%}
!!€ кельк%>000

!2016.QQ
! Suffix final hard vs soft
"%{ЬØ%}:ь"
!! **@RULENAME@**  
%{ЬØ%}:ь <=> [ ь:|й:] ([%{ЕØ%}:] %^RmVow:) %> (%{Х%}:) т:  _ ;
 [ ь:|й:| [# Cns* я |и|е|э|ы] [CnsNonDent:]] ([%{ЕØ%}:] %^RmVow:) %> (%{Х%}:)  %{КТ%}:  _ ;

!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть
!! симомс+V+Imprt+ScSg2
!!€ сим%>%{Х%}%{КТ%}%{ЬØ%}
!!€ сип%>0ть
!!$ сим%>%{Х%}%{КТ%}%{ЬØ%}
!!$ сип%>0т0

"%{ЬØ%}:0"
!! **@RULENAME@**  
%{ЬØ%}:0 <=> 
 [ Cns: - [ь:|й:]] ([%{ЕØ%}:|%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> (%{Х%}:) т:  _ ;
!! зепе+N+Pl+Nom+Indef: **pocket/tasku**
!!€ зеп%{ЕОØ%}%^RmVow%>%{Х%}т%{ЬØ%}
!!€ зеп00%>0т0

 [CnsDent: |[Vows - [я|и|е|э|ы]|Vows Cns+ я ] [CnsNonDent:]|CnsAndSoft [CnsNonDent:] |Vows] ([%{ЕØ%}:] %^RmVow:) %> (%{Х%}:)  %{КТ%}:  _ ;
 :Vows (%^Я2А:) %> %{Х%}: т _ ;
 ! :BTV %> %{Х%}: т _ ;
!! ши+N+Pl+Nom+Indef: **day/päivä**
!!€ ши%>%{Х%}т%{ЬØ%}
!!€ ши%>0т0
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть
!! вальмя+N+Pl+Nom+Indef: **window/ikkuna**
!!€ вальмя%^Я2А%>%{Х%}т%{ЬØ%}
!!€ вальма0%>0т0


!! Plural before definite plural following a consonant
"t:0"
!! **@RULENAME@**  
т:0 <=>
[б:|в:|ж:|з: ( ь: )| й: |г:|к| л ( ь: ) |м: |п| р ( ь: ) |с ( ь: ) |ф|х|ц ( ь: ) |ч|ш|щ] ([%{ЕОØ%}:|%{АОØ%}:] %^RmVow:) %> %{Х%}: _ н е: ;
!! Ананьина К.И. 2000: 55
!!€ ава%>%{Х%}тне
!!€ ава%>0тне
!!€ шуфт%>%{Х%}тне
!!€ шуфт%>0тне
!!€ нармот0%>0тне
!!€ лав%>%{Х%}тне
!!€ лаф%>00не
!!$ лав%>%{Х%}тне
!!$ лаф%>0тне

![б:|д: ( ь: ) |ж:|з: ( ь: )| й |г:|к| л ( ь: ) |м:|н: ( ь: ) |п| р ( ь: ) |с ( ь: ) |т ( ь: ) |ф|х|ц ( ь: ) |ч|ш|щ] %> %{Х%}: _ н е: ;

!!SLoss s s > s = s 0 0 s
"s:0"
!! **@RULENAME@**  
с:0 <=> с _ %> SLoss:0 с ;

!!€ класс%>SLossс
!!€ клас000с
!2016.03!




!Word final Vowels

!2016.QQ
"y:0"
!! **@RULENAME@**  
 ь:0 <=> 
  [ н: | д: | т | з: ] _ (%{ЕØ%}: %^RmVow:) %> (%{Х%}:) [ т: | %{КТ%}: ] ; 
!! plural
!!€ пань%>%{КТ%}%{ЬØ%}
!!€ пан0%>ть
  [ н | р | д | з ] _  %> %{DIM%}: я ; 
!! ведь+N+Der+Der/Dimin+N+Sg+Nom+Indef: **a little water**
!!€ ведь%>%{DIM%}я
!!€ вед0%>ня
 CnsDent: _ ( %>|%^PXSG1: %>) [ %{АЯ%}: | %{ИЫЙ%}: | %{ЕØ%}:е | %{ЕОØ%}: | (%^Ь2ZERO: %> ) %{ВИУ%}: ] ;
!!€ мяль%>%{ЕОØ%}зе
!!€ мял0%>езе
!! мирде+N+Sg+Nom: **husband/aviomies**
!!€ мирдь%{ЕØ%}#
!!€ мирд0е#
[ CnsDent: - [ л: | р: ] ] _ %^PXSG1: %> н е ;
!! кядь+N+Pl+Nom+PxSg1: **hand; arm**
!!€ кядь%^PXSG1%>не
!!€ кяд00%>не

[ч|ш|ж] _ %> с ;
!! учемс+V+Ind+Prt1+ScSg3
!!€ учь%>сь
!!€ уч0%>сь

[ CnsDent: - [ л: ] ] _ %> ц е ;
!! нярь+N+Sg+Gen+PxSg2: **hand; arm**
!!€ нярь%>це
!!€ няр0%>це

    _ %> [ и | %{АЕØ%}:е ] ;
!    _ %> %{АЕØ%}:е ;
!!€ афоль%>ине
!!€ афол0%>ине

 CnsDentNoL _ %> [ф т %{ЕОØ%}: м [ %{ЕОØ%}: ] | К1: | н | т | с: (т) [ ь: | е: ]] ;
!2018-07-01 CnsDentNoL _ %> [ф т %{ЕОØ%}: м [ %{ЕОØ%}: ] | К1: | н | д: | т | с: (т) [ ь: | е: ]] ;
 Cns: _ (%^Devoice:) %^Ь2ZERO: ;
!! изь+V+Aux+Neg+Ind+Prt1+ScPl3: **they did not**
!!€ изь%^Devoice%^Ь2ZERO%>ть
!!€ ис000%>ть
!2016.QQ
!! ломань+N+SP+Abl+Indef:
!!$ ломань%>д%{АО%}
!!$ ломань%>да

!! ### DIMINUTIVES
"%{DIM%}:н"
!! **@RULENAME@**
 %{DIM%}:н <=> [ Vows: | :Vows | %{ЕОØ%}:|%{АОØ%}:| м  | ж | б | в | н | д | л | р  | з | :й] (ь:)  %> _ я ;
!!€ ши%>%{DIM%}я
!!€ ши%>ня
!!€ ара%>%{ИЫЙ%}%>%{DIM%}я
!!€ ара%>й%>ня
        к %> %{ЕО%}: _ я ;

"%{DIM%}:к"
!! **@RULENAME@**
 %{DIM%}:к <=> [ ф | т | ц | ч | :с | ш | х ] (ь) %> _ я: ;
!!€ пикс>%{DIM%}я
!!€ пикс0кя


!!Allow the absence of "ъ" in the first syllable
!"Loss of ъ in the first syllable orthography"
!ъ:0 <= # Cns* _ [ д | з | р | т ] ; REMOVE 2018-09-12

"%{ъØ%}:ъ"
!! **@RULENAME@**
 %{ъØ%}:ъ <=>
 [# | Cns: ] _ \[ # ]* %^Ъ2PED: ;


!! ## Disallow

"Disallow TLoss after non-t"
!! **@RULENAME@**  
TLoss:0 /<= [[ :* Letters ] - [ :* :т ] ] %> _ ;

"Disallow KLoss after non-k"
!! **@RULENAME@**  
KLoss:0 /<= [[ :* Letters ] - [ :* :к ] ] %> _ ;

"Disallow SLoss after non-s"
!! **@RULENAME@**  
SLoss:0 /<= [[ :* Letters ] - [ :* :с ] ] %> _ ;

!"Disallow VLoss after non-U2I2"
!!! __@RULENAME@__  
!VLoss:0 /<= [[ :* [ Letters | А1: | %{АЯ%}: | %{ИЫЙ%}: | К1:  | %{ЬØ%}: | %{гк%}: | %{вгк%}: | К3:  ] ] - [ :* [ У2: | И2: ] ] ] %> _ ;


!"Disallow translative linking vowel vs no linking vowel "
!!! __@RULENAME@__
! k:k /<= [[ :* Letters ] - [ :*  к ]] %> %{ЕОØ%}: _ с  ;
!          к  %>  _ с  ;

"Disallow овок"
!! **@RULENAME@**
в:в /<=
    	[Cns|Vow] [б|в|г|д|ж|з|й:|л|м|н|п|р|с|ф|х|ц|ш|щ] (ь:) %> %{ЕОØ%}: _ о к ;
    	%> [с |т|%{ЕОØ%}: с т|%{ЕОØ%}: ц|%{ЕОØ%}: н|%{ЕОØ%}: н ь к о н ] (ь:) %> %{ЕОØ%}: _ о к ;

"Disallow Онга"
!! **@RULENAME@**
н:н /<=
    	%> [ш к а] %> %{ЕОØ%}: _ г а ;
        %{ЕОØ%}: _ к %> (н) %{гк%}: %{АЕ%}: ;
        %{ЕОØ%}: _ %> н г %{АЕ%}: ;
        н е %> _ г %{АЕ%}: ; !Pl+Nom+Def/PxSg1
!        %{ЕОØ%}: _ [ к|т ] %> (н) %{гк%}: %{АЕ%}: ;
!!$ куд>{ЕОØ}нз{АО}>{ЕОØ}н>нг{АЕ}
!!$ куд>онзо>0н>нга

"Disallow -кс: нетькскс"
!! **@RULENAME@**
к:к /<=
       к с %> _ с ;
       к [Cns - [c]] (ь:) %> %{ЕОØ%}: _ с ;
       [Vows|Cns - [с|ц|ш|ч|д|т]] (ь:) %> %{ЕО%}: %> ш _ а ;
       Vows %> %{ЕОØ%}: _ с ;
       [Cns - [т|к|п|с]] (ь:) %> %{ЕОØ%}: в о _ ;

"Disallow -гя only after a few"
!! **@RULENAME@**
я:я /<=
     [[:Vows - [:и]] (%^Я2А:) |[Cns - [й]]|[Cns - [л|н|з|р|д]] ь:] %> г _ [# | %- |%>];
      [Cns - к] %> %{ЕО%}: %{DIM%}: _ ;
      _ %> %{ЕОØ%}: в а ;

"Disallow special imperative in K after vowel stems"
!! **@RULENAME@**
%{Х%}:0 /<= Vows %> _ к ;

"Disallow final е lowering to я tag elsewhere"
!! **@RULENAME@**
%^Е2Я:0 /<= [[:Letters - [:е|е:|%{ЕØ%}:я|%{ЕО%}:я|%{ЕОØ%}:я|%{АЕ%}:я]]|#|%>|:0] _ ;

"Disallow onset vowel after voiced cons"
!! **@RULENAME@**
%^PXSG1:0 /<=
     [г|й:|к|п|ф|х|ш|щ|ч|[т|с] (ь:) ] _ %>  н е [%>|# | %- ] ;
     [Vows:|Vows:|б|в|ж|м|[д|з|л|н|р] (ь:) ] _ %> %{ЕОØ%}: н е [%>|# | %- ] ;

!!TESTS
!echo 'п а н ж:ч > с ы' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'панжемс+V+Ind+Prs+ScSg3+OcSg3' | hfst-lookup src/fst/lexicon.hfst
!echo 'с и м:п > {Х}:0 {КТ}:т {ЬØ}:ь' | hfst-pair-test src/fst/phonology.compose.hfs
