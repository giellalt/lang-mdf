!! Proper noun inflection

!! The MOKSHA language proper nouns inflect in the same cases as regular
!! nouns, but with a colon (':') as separator.

!LEXICON PropTag
! PROP_ ;
LEXICON PROP_
+Sg+Nom+Indef: K ;
+Sg+Nom+Def:%>сь K ;
+Sg+Gen+Indef:%>нь K ;
+Sg+Gen+Def:%>ть K ;
+Sg+Dat+Def:%>ти K ;

LEXICON PROP_SARAZ
: N_SARAZ ;
LEXICON PROP-VOC_KIAJ
+Voc:%^Devoice%^Ь2ZERO%>кяй ENDLEX ;

LEXICON PROP_KAL
: N_KAL	;

LEXICON PROP_KIAL1
: N_KIAL1	;

LEXICON PROP_OSH
: N_OSH	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Male given name for deriving patronyms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Андрей:Андре
LEXICON PropNameMaleDer-J-0Evich
+Der/PatrMal+Orth/Colloq:ич PROP_OSH_PATRMAL ;
+Der/PatrMal:евич PROP_OSH_PATRMAL ;
+Der/PatrFem:евна PROP_KUDO_PATRFEM ;
:й PROP_KAL_MAL ;

!LEXICON PROP-PATR-MAL_END-IN-CH/DZ ! from urj-Cyrl-propernouns.lexc
! PROP_OSH_PATRMAL ;

LEXICON PropNameMaleDer-IJ-Y0Evich
+Der/PatrMal+Orth/Colloq:ьич PROP_OSH_PATRMAL ;
+Der/PatrMal:ьевич PROP_OSH_PATRMAL ;
+Der/PatrFem:ьевна PROP_KUDO_PATRFEM ;
:ий PROP_KAL_MAL ;

LEXICON PropNameMaleDer-IJ-I0Evich
+Der/PatrMal+Orth/Colloq:ьич PROP_OSH_PATRMAL ;
+Der/PatrMal:иевич PROP_OSH_PATRMAL ;
+Der/PatrFem:иевна PROP_KUDO_PATRFEM ;
:ий PROP_KAL_MAL ;

LEXICON PropNameMaleDer-Y-0Evich
+Der/PatrMal+Orth/Colloq:ич PROP_OSH_PATRMAL ;
+Der/PatrMal:евич PROP_OSH_PATRMAL ;
+Der/PatrFem:евна PROP_KUDO_PATRFEM ;
:ь PROP_KAL_MAL ;

!! Вили:Вил
LEXICON PropNameMaleDer-I-YEvich
+Der/PatrMal+Orth/Colloq:ич PROP_OSH_PATRMAL ;
+Der/PatrMal:ьевич PROP_OSH_PATRMAL ;
+Der/PatrFem:ьевна PROP_KUDO_PATRFEM ;
:и PROP_KAL_MAL ;

LEXICON PropNameMaleDer-Ovich
+Der/PatrMal+Orth/Colloq:ыч PROP_OSH_PATRMAL ;
+Der/PatrMal:ович PROP_OSH_PATRMAL ;
+Der/PatrFem:овна PROP_KUDO_PATRFEM ;
 PROP_KAL_MAL ;

!!!!!!!!!!!!!!!!!!!!!!!!!
!! Russian type Surnames 
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Абдеев:Абдеев
LEXICON Deriv-RUS-V_SURMAL
: PROP_KAL_SURMAL ;
:а PROP_KUDO_SURFEM ;

!! Багрий:Багр
LEXICON Deriv-RUS-IJ_SURMAL
:ий PROP_KAL_SURMAL ;
:яя PROP_KUDO_SURFEM ;

!! Аморский:Аморск
LEXICON Deriv-RUS-KIJ_SURMAL
:ий PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-OJ_SURMAL
:ой PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-YJ_SURMAL
:ый PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-AN_SURMAL
 Deriv-RUS-V_SURMAL ;

LEXICON Deriv-RUS-IN_SURMAL
 Deriv-RUS-V_SURMAL ;

LEXICON PROP_KAL_SURMAL
+Sem/Mal-Sur: NMN_KAL ;

LEXICON PROP_KUDO_SURFEM
+Sem/Fem-Sur: NMN_AVA ;

LEXICON CYRL-CONS_SUR
 PROP_KAL_SUR ;

LEXICON PROP_KAL_SUR
+Sem/Sur: NMN_KAL ;


LEXICON CYRL-T_SUR
 PROP_KIT_SUR ;

LEXICON PROP_KIT_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-L_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-K_SUR
 PROP_ЛАК_SUR ;

LEXICON PROP_ЛАК_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-SIBILANT_SUR
 PROP_OSH_SUR ;

LEXICON PROP_OSH_SUR
+Sem/Sur: NMN_OSH ;
!+Sem/Sur: osh-N-Derivation ;
!+Sem/Sur: osh-N-Def-Declension ;
!+Sem/Sur: osh-Indef-Declension ;
!+Sem/Sur: osh-N-Poss-Declension-CX ;

LEXICON CYRL-VOW_SUR
 PROP_КУДО_SUR ;

LEXICON CYRL-A_SUR
 PROP_КУДО_SUR ;

LEXICON PROP_КУДО_SUR
+Sem/Sur: NMN_AVA ;

LEXICON PROP_KAL_MAL
+Sem/Mal: NMN_KAL ;

LEXICON PROP_LAK_MAL
+Sem/Mal: NMN_KAL ;

LEXICON PROP_KIT_MAL
+Sem/Mal: NMN_KAL ;

LEXICON PROP_OSH_MAL
+Sem/Mal: NMN_OSH ;

LEXICON PROP_KUDO_MAL
+Sem/Mal: NMN_AVA ;


LEXICON PROP_OSH_PATRMAL
+Sem/Mal-Patr: NMN_OSH ;

LEXICON PROP_KUDO_PATRFEM
+Sem/Fem-Patr: NMN_AVA ;


LEXICON PROP_KAL_FEM
+Sem/Fem: NMN_KAL ;

!LEXICON PROP_ЛАК_FEM
!+Sem/Fem: NMN_KAL ;

LEXICON PROP_KIT_FEM
+Sem/Fem: NMN_KAL ;

LEXICON PROP_OSH_FEM
+Sem/Fem: NMN_OSH ;

!LEXICON PROP_ТЕШТЕ_FEM
! PROP_ПАНГО_FEM ;

!LEXICON PROP_ТИНГЕ_FEM
! PROP_ПАНГО_FEM ;

!LEXICON PROP_ПАНГО_FEM
!+Sem/Fem: NMN_AVA ;

LEXICON PROP_KUDO_FEM
+Sem/Fem: NMN_AVA ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! PLACE NAMES FROM TEMPLATES 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP-PLC_KAL !!≈ * __@CODE@__ ending in other consonant
+Sem/Plc: NMN_KAL ;

LEXICON PROP-PLC_KIT !!≈ * __@CODE@__ ending in t
+Sem/Plc: NMN_KAL ;

!LEXICON PROP-PLC_OSH !!≈ * __@CODE@__ ending in shibilant
!+Sem/Plc: NMN_OSH ;

LEXICON PROP-PLC_KUDO !!≈ * __@CODE@__ ending in vowel
+Sem/Plc: NMN_AVA ;



!!!!!!!!!!!!!!!!
!! PROPER NOUNS 
!!!!!!!!!!!!!!!!
LEXICON PROP-PLC_AVA !!≈ * __@CODE@__ ending in vowel
+Sem/Plc: NMN_AVA ;
LEXICON PROP-PLC_VAJ !!≈ * __@CODE@__ ending in vowel
+Sem/Plc: NMN_VAJ ;


LEXICON PROP_AVA !!≈ * __@CODE@__ ending in vowel
: NMN_AVA ;

LEXICON PROP_VAJ !!≈ * __@CODE@__ ending in vowel
: NMN_VAJ ;

LEXICON PROP_KELU !!≈ * __@CODE@__ ending in u
: NMN_KELU ;

LEXICON PROP_ANDY !!≈ * __@CODE@__ ending in i
: NMN_ANDY ;


LEXICON PROP
+Sg+Gen+Def:%>ть ENDLEX ;
+Sg+Nom+Def:%>сь ENDLEX ;
+SP+Gen+Indef:%>нь ENDLEX ;
+SP+Ine+Indef:%>са ENDLEX ;
+SP+Lat+Indef:%>у ENDLEX ; !АССР-у

LEXICON MOKSHA-MAL/ORJASJ_AVA
+Sem/Mal: N_AVA ;
:%>%{ЕОØ%}РЯ PROP_ORJASJ ;
LEXICON MOKSHA-MAL/ORJASJ_KAL
+Sem/Mal: N_KAL ;
:%>%{ЕОØ%}ря PROP_ORJASJ ;
LEXICON MOKSHA-MAL/ORJASJ_KELU
+Sem/Mal: N_KELU ;
:%>%{ЕОØ%}ря PROP_ORJASJ ;
:в%>%{ЕОØ%}ря PROP_ORJASJ ;
LEXICON MOKSHA-MAL/ORJASJ_OSH
+Sem/Mal: N_OSH ;
:%>%{ЕОØ%}ря PROP_ORJASJ ;
LEXICON MOKSHA-MAL/ORJASJ_VAJ
+Sem/Mal: N_VAJ ;
:%>%{ЕОØ%}ря PROP_ORJASJ ;
:%>ря PROP_ORJASJ ;
LEXICON PROP_ORJASJ
+Der/Wife+Sem/Fem+Sg+Dat+Def:%>ти K ;
+Der/Wife+Sem/Fem+Sg+Gen+Def:%>ть K ;
+Der/Wife+Sem/Fem+Sg+Nom+Def:%>сь K ;
+Der/Wife+Sem/Fem+Sg+Nom+Indef: K ;


LEXICON ACRONDELIMITER ! Here we need some kind of vowel harmony
+Sg+Nom+Indef: ENDLEX ;
:%- PROP ;
!:%: PROP ;
!:%' PROP ;




! vim: set ft=xfst-lexc:

