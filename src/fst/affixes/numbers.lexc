!! Quantifier inflection

!! Numerals in the MOKSHA language inflect in the same cases as regular
!! nouns.

!!!!!!!!!!!!
!! NUMERALS
!!!!!!!!!!!!
LEXICON NUMERAL-TYPES
фкя+Num+Card:фкя NMN_VAL1MIA ;
фкя+Num:васен NUMERALS-TO-ORD ;
фкя+Num+Der+Der/Iter+Adv+Iter:весть ENDLEX ;
фкя+Num:фи NUMERALS-FOR-COLL ;

кафта+Num+Card:кафт%{АОØ%} N_OFTA ;
кафта+Num:омбо NUMERALS-TO-ORD ;
кафта+Num:каф NUMERALS-TO-ITER ;
кафта+Num:кафо NUMERALS-FOR-ASSOCCOLL ;
кафта+Num:кафо NUMERALS-FOR-COLL ;
кафта+Num+Coll+Sg+Nom:кафцьке ENDLEX ;
кафта+Num+Coll+Sg+Gen:кафцьконь K ;
кафта+Num+Coll+Sg+Dat:кафцьконди K ;
кафта+Num+Der+Der/Ord+A+Ord:омбо NUMERALS-FOR-COLL ;

колма+Num:колм%{АО%} NUMERALS-3-AND-UP-PULA ;

ниле+Num:ниле NUMERALS-3-AND-UP-PINE ;
вете+Num:вете NUMERALS-3-AND-UP-PINE ;
кота+Num:кот%{АО%} NUMERALS-3-AND-UP-PULA ;
сисем+Num:сисем NUMERALS-3-AND-UP-KIAL1 ;
кафкса+Num:кафкс%{АО%} NUMERALS-3-AND-UP-PULA ;
вейхкса+Num:вейхкс%{АО%} NUMERALS-3-AND-UP-PULA ;
кемонь+Num:кемонь NUMERALS-3-AND-UP-KIAL1 ;
кефкие+Num:кефкие  NUMERALS-3-AND-UP-PINE ;
кемгафтува+Num:кемгафтув%{АО%} NUMERALS-3-AND-UP-PULA ;
кемголмува+Num:кемголмув%{АО%} NUMERALS-3-AND-UP-PULA ;
кемнилие+Num:кемнилие  NUMERALS-3-AND-UP-PINE ;
кеветие+Num:кеветие  NUMERALS-3-AND-UP-PINE ;
кемготува+Num:кемготув%{АО%} NUMERALS-3-AND-UP-PULA ;
кемзисемге+Num:кемзисемг%{ЕО%} NUMERALS-3-AND-UP-SEL1ME ;
кемгафксува+Num:кемгафксув%{АО%} NUMERALS-3-AND-UP-PULA ;
кевейхсые+Num:кевейхсые  NUMERALS-3-AND-UP-PINE ;
комсь+v1+Num:комсь NUMERALS-3-AND-UP-OSH ;
комсь+v2+Num:комозь NUMERALS-3-AND-UP-OSH ;
колмогемонь+Num:колмогемонь NUMERALS-3-AND-UP-KIAL1 ;
ниленьгемонь+Num:ниленьгемонь NUMERALS-3-AND-UP-KIAL1 ;
ведьгемонь+Num:ведьгемонь NUMERALS-3-AND-UP-KIAL1 ;
кодгемонь+Num:кодгемонь NUMERALS-3-AND-UP-KIAL1 ;
сизьгемонь+Num:сизьгемонь NUMERALS-3-AND-UP-KIAL1 ;
кафксоньгемонь+Num:кафксоньгемонь NUMERALS-3-AND-UP-KIAL1 ;
вейхксоньгемонь+Num:вейхксоньгемонь NUMERALS-3-AND-UP-KIAL1 ;

LEXICON NUMERALS-3-AND-UP-PINE
+Card: N_PINE ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;
LEXICON NUMERALS-3-AND-UP-PULA
+Card: NMN_PULA ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;
LEXICON NUMERALS-3-AND-UP-SEL1ME
+Card: NMN_SEL1ME ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;
LEXICON NUMERALS-3-AND-UP-KIAL1
+Card: N_KIAL1 ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;
LEXICON NUMERALS-3-AND-UP-OFTA
+Card: N_OFTA ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;
LEXICON NUMERALS-3-AND-UP-OSH
+Card: NMN_OSH ;
NUMERALS-ASSOCCOLL/COLL/ITER/ORD ;

LEXICON NUMERALS-ASSOCCOLL/COLL/ITER/ORD
 NUMERALS-FOR-ASSOCCOLL ;
 NUMERALS-FOR-COLL ;
 NUMERALS-TO-ITER ;
 NUMERALS-TO-ORD ;

LEXICON NUMERALS-FOR-ASSOCCOLL
+AssocColl+PxSg1:%>нен ENDLEX ;
+AssocColl+PxSg2:%>нет ENDLEX ;
+AssocColl+PxSg3:%>ненза ENDLEX ;

+AssocColl+PxPl1:%>нек ENDLEX ;
+AssocColl+PxPl1:%>ненк ENDLEX ;
+AssocColl+PxPl2:%>нент ENDLEX ;
+AssocColl+PxPl3:%>нест ENDLEX ;

LEXICON NUMERALS-FOR-COLL
+Coll+Sg+Nom:%>%{ЕОØ%}нст ENDLEX ;
+Coll+Sg+Nom:%>%{ЕОØ%}цьке ENDLEX ;
+Coll+Sg+Gen:%>%{ЕОØ%}цьконь K ;
+Coll+Sg+Dat:%>%{ЕОØ%}цьконди K ;

LEXICON NUMERALS-TO-ITER
+Der+Der/Iter+Adv+Iter:%>ксть ENDLEX ;
LEXICON NUMERALS-TO-ORD
+Der+Der/Ord+A+Ord:%>це NMN_AVA ;
+Der+Der/Ord+A+Ord+Der+Der/Dimin+N:%>це%>ня NMN_AVA ;

LEXICON IS_NUM_ORD !вейхксоце
+Ord: NMN_AVA ;

LEXICON IS_NUM_ORD_ITER !вейхксоцеда
+Ord+Iter: K ;

!!!!!!!!!!!
!! NUMBERS 
!!!!!!!!!!!

!LEXICON QNT_
!: K ;

LEXICON NUM_
: K ;

LEXICON NUM-CARD_
+Card: K ;

LEXICON NUM-CARD_AVA
+Card: NMN_AVA ;

LEXICON NUM-CARD_PULA
+Card: NMN_PULA ;

LEXICON NUM-CARD_KAL
+Card: NMN_KAL ;


LEXICON NUM-COLL_
+Card+Coll:	K ;

LEXICON NUM_CARD_COLL_NCT !!комсенцт:комсенц
+Card+Coll: PL-NOM-INDEF ;
+Card+Coll: PL-DAT/GEN/NOM-DEF ;

LEXICON NUM_ORD_COLL_NCT !! омбонцт:омбонц
+Ord+Coll: PL-NOM-INDEF ;
+Ord+Coll: PL-DAT/GEN/NOM-DEF ;

!LEXICON NUM-ITER_
!+Mult: K ;

LEXICON NUM-ORD_
+Ord: K ;


! These lexica are for giella-shared/smi/src/fst/digits.lexc
LEXICON dateyearcase
+Sg+Nom: # ;

LEXICON dateyearcase_nullsuff_w_dot
+Sg+Nom: MARKDOT ;

LEXICON dateyearcase_fullsuff
+Sg+Gen:%-нь # ;
+Sg+Cmpr:%-шка # ;

LEXICON ARABICCASES
+Arab: DIGITCASES ;

LEXICON ARABICCASE   !!≈ * **@CODE@**  adds +Arab
 +Arab:  DIGITCASE ;

LEXICON ARABICCASE0   !!≈ * **@CODE@**  adds +Arab
 +Arab:  DIGITCASE0 ;

LEXICON DIGITCASE
DIGITCASE0 ;
DIGITCASES ;

LEXICON DIGITCASE0
      +Sg+Nom: # ;

LEXICON DIGITCASES

  +Sg+Gen:%-нь # ;
  +Cmp/SplitR:%:%- ENDLEX ;

LEXICON ARABICCASEORD-ERR
ARABICCASEORD ;
LEXICON ARABICCASEORD
+Arab+Sg+Nom:це # ;

LEXICON ARABICCASECOLL
+Arab+Pl+Nom+Indef:%>-т      # ;
+Arab+Sg+Nom+Indef:%>-це # ;!This is an ordinal 1975-це

LEXICON ARABICCOMPOUNDS   !!≈ * **@CODE@**  ! 1-osainen
+Use/NG: NounEnd ; ! 3-

LEXICON ROMNUMTAGOBL !
# ;
!      +Rom+Sg+Gen:n   # ; ! But here 123a 123

LEXICON yeartagpl
          +Num+Sem/Year+Pl+Nom:        # ;


! vim: set ft=xfst-lexc:

